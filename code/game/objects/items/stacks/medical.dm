/obj/item/stack/medical
	name = "medical pack"
	singular_name = "medical pack"
	icon = 'icons/obj/stack_objects.dmi'
	amount = 12
	max_amount = 12
	w_class = WEIGHT_CLASS_TINY
	full_w_class = WEIGHT_CLASS_TINY
	throw_speed = 3
	throw_range = 7
	resistance_flags = FLAMMABLE
	max_integrity = 40
	novariants = FALSE
	item_flags = NOBLUDGEON
	var/self_delay = 50
	var/other_delay = 0
	var/repeating = FALSE
	/// Сколько физического урона лечим за применение
	var/heal_brute
	/// Сколько урона от ожогов лечим за применение
	var/heal_burn
	/// Насколько уменьшаем кровотечение за применение при порезах
	var/stop_bleeding
	/// Сколько санитизации применяем к ожогам за применение
	var/sanitization
	/// Сколько добавляем к flesh_healing для ожоговых ран за применение
	var/flesh_regeneration
	var/heal_dead = FALSE // можем ли мы лечить мёртвое тело
	var/heal_dead_multiplier = 1 // Эффективность лечения мёртвых
	/// Игнорирование БРОНИ. Не забывайте, пожалуйста, добавлять bypass_armor = TRUE в предметы, которые должны игнорвироать броню. Т.е. скафандры/= броня.
	var/bypass_armor = FALSE

/obj/item/stack/medical/attack(mob/living/M, mob/user)
	. = ..()
	INVOKE_ASYNC(src, PROC_REF(begin_heal_loop), M, user, TRUE)

/// Используется для запуска рекурсивного цикла лечения. Возвращает TRUE если мы вошли в цикл, FALSE если нет
/obj/item/stack/medical/proc/begin_heal_loop(mob/living/patient, mob/living/user, auto_change_zone = TRUE)
	if(INTERACTING_WITH(user, patient))
		return FALSE
	if(iscarbon(patient))
		var/mob/living/carbon/carbon_patient = patient
		if(!has_healable_damage(carbon_patient))
			patient.balloon_alert(user, "нечего лечить")
			return FALSE

	var/heal_zone = check_zone(user.zone_selected)

	if(!heal_dead && patient.stat == DEAD && !HAS_TRAIT(patient, TRAIT_UNDEAD))
		patient.balloon_alert(user, "мёртв!")
		return FALSE

	// Проверяем выбранную зону
	if(!try_heal_checks(patient, user, heal_zone, silent = TRUE))
		// Если выбранная зона не нуждается в лечении и включен автоматический режим
		if(iscarbon(patient) && auto_change_zone)
			var/original_zone = heal_zone
			var/was_armored = FALSE
			var/armor_is_spacesuit = FALSE
			if(ishuman(patient))
				var/mob/living/carbon/human/H = patient
				var/obj/item/bodypart/selected_bp = H.get_bodypart(original_zone)
				if(selected_bp && (selected_bp.get_damage() > 0 || has_treatable_wounds_on(selected_bp)))
					var/obj/item/clothing/covering = get_bodypart_protecting_clothing_by_coverage(H, selected_bp)
					if(covering && (covering.clothing_flags & THICKMATERIAL))
						was_armored = TRUE
						armor_is_spacesuit = istype(covering, /obj/item/clothing/suit/space)
			// Ищем любую поврежденную часть тела
			var/mob/living/carbon/carbon_patient = patient
			var/list/damaged_limbs = list()
			for(var/obj/item/bodypart/limb as anything in carbon_patient.bodyparts)
				if(try_heal_checks(patient, user, limb.body_zone, silent = TRUE))
					damaged_limbs += limb.body_zone

			if(!length(damaged_limbs))
				if(carbon_patient.getBruteLoss_nonProsthetic() > 0 || carbon_patient.getFireLoss_nonProsthetic() > 0)
					if(ishuman(patient))
						var/mob/living/carbon/human/H_bl = patient
						var/list/blocked_suit_bl = list()
						var/list/blocked_armor_bl = list()
						for(var/obj/item/bodypart/bl_limb as anything in H_bl.bodyparts)
							if(!bl_limb.get_damage() && !has_treatable_wounds_on(bl_limb))
								continue
							var/obj/item/clothing/bl_cover = get_bodypart_protecting_clothing_by_coverage(H_bl, bl_limb)
							if(bl_cover && (bl_cover.clothing_flags & THICKMATERIAL))
								if(istype(bl_cover, /obj/item/clothing/suit/space))
									blocked_suit_bl += ru_parse_zone(bl_limb.body_zone)
								else
									blocked_armor_bl += ru_parse_zone(bl_limb.body_zone)
						if(length(blocked_suit_bl))
							patient.balloon_alert(user, "[blocked_suit_bl.Join(", ")] закрыт[length(blocked_suit_bl) > 1 ? "ы" : "а"] скафандром!")
						else if(length(blocked_armor_bl))
							patient.balloon_alert(user, "[blocked_armor_bl.Join(", ")] закрыт[length(blocked_armor_bl) > 1 ? "ы" : "а"] бронёй!")
					return FALSE
				patient.balloon_alert(user, "полностью здоров[patient.ru_a()]")
				return FALSE

			heal_zone = damaged_limbs[1]
			if(was_armored)
				patient.balloon_alert(user, "[ru_parse_zone(original_zone)] в [armor_is_spacesuit ? "скафандре" : "броне"], лечим [ru_parse_zone(heal_zone)]...")
			else
				patient.balloon_alert(user, "лечим [ru_parse_zone(heal_zone)]...")
		else
			// В ручном режиме или для не-карбонов просто выходим
			return FALSE
	else
		// Выбранная часть тела повреждена, начинаем лечение
		pass()

	INVOKE_ASYNC(src, PROC_REF(try_heal), patient, user, heal_zone, FALSE, iscarbon(patient) && auto_change_zone)
	return TRUE

/**
 * Процедура, которая обрабатывает вывод сообщения о начале лечения и саму попытку лечения
 * Эта процедура рекурсивно вызывается, пока не закончатся заряды ИЛИ пока пациент не будет полностью вылечен
 * ИЛИ пока целевая зона не будет полностью вылечена (если auto_change_zone = FALSE)
 *
 * Аргументы:
 * * patient - моб, которого мы пытаемся вылечить
 * * user - моб, который пытается вылечить пациента
 * * healed_zone - зона, которую мы пытаемся вылечить на пациенте. Игнорируется если auto_change_zone = TRUE
 * * silent - если TRUE, не выводим сообщение о начале лечения пациента
 * * auto_change_zone - управляет поведением когда мы заканчиваем лечение зоны
 *   Если TRUE, выбирает следующую наиболее повреждённую зону. Если FALSE, даёт пользователю возможность выбрать новую зону
 * * continuous - если установлено в TRUE, будет проигрываться непрерывный звук лечения
 */
/obj/item/stack/medical/proc/try_heal(mob/living/patient, mob/living/user, healed_zone, silent = FALSE, auto_change_zone = TRUE, continuous = FALSE)
	if(patient == user)
		if(!silent)
			user.visible_message("<span class='notice'>[user] начинает наносить \the [src] на себя...</span>", "<span class='notice'>Вы начали наносить \the [src] на себя...</span>")
		if(!do_mob(user, patient, self_delay * (auto_change_zone ? 1 : 0.9), extra_checks=CALLBACK(src, PROC_REF(can_heal), patient, user, healed_zone)))
			return
		if(!auto_change_zone)
			healed_zone = check_zone(user.zone_selected)
		if(!try_heal_checks(patient, user, healed_zone))
			return
	else if(other_delay)
		if(!silent)
			user.visible_message("<span class='notice'>[user] начинает наносить \the [src] на [patient].</span>", "<span class='notice'>Вы начали наносить \the [src] на [patient]...</span>")
		if(!do_mob(user, patient, other_delay * (auto_change_zone ? 1 : 0.9), extra_checks=CALLBACK(src, PROC_REF(can_heal), patient, user, healed_zone)))
			return
		if(!auto_change_zone)
			healed_zone = check_zone(user.zone_selected)
		if(!try_heal_checks(patient, user, healed_zone))
			return
	else
		if(!silent)
			user.visible_message("<span class='notice'>[user] наносит \the [src] на [patient].</span>", "<span class='notice'>Вы наносите \the [src] на [patient].</span>")

	if(iscarbon(patient))
		if(!heal_carbon_new(patient, user, healed_zone))
			return
	else if(isanimal(patient))
		if(!heal_animal(patient, user))
			return
	else
		return

	if(!use(1) || !repeating || amount <= 0)
		return

	log_combat(user, patient, "healed", src.name)

	// first, just try looping - we can keep healing the current target or user changed their target
	var/preferred_target = check_zone(user.zone_selected)
	if(try_heal_checks(patient, user, preferred_target, silent = TRUE))
		if(preferred_target != healed_zone)
			patient.balloon_alert(user, "переключаем на [ru_parse_zone(preferred_target)]...")
		try_heal(patient, user, preferred_target, TRUE, auto_change_zone, TRUE)
		return

	// second, handle what happens otherwise
	if(!iscarbon(patient))
		// behavior 0: non-carbons have no limbs so we can assume they are fully healed
		patient.balloon_alert(user, "полностью вылечен[patient.ru_a()]")
	else if(auto_change_zone)
		// behavior 1: automatically pick another zone to heal
		try_heal_auto_change_zone(patient, user, preferred_target, healed_zone)
	else
		// behavior 2: assess injury, giving the user time to manually pick another zone
		try_heal_manual_target(patient, user)

/obj/item/stack/medical/proc/try_heal_auto_change_zone(mob/living/carbon/patient, mob/living/user, preferred_target, last_zone)
	var/list/other_affected_limbs = list()
	for(var/obj/item/bodypart/limb as anything in patient.bodyparts)
		if(!try_heal_checks(patient, user, limb.body_zone, silent = TRUE))
			continue
		other_affected_limbs += limb.body_zone

	if(!length(other_affected_limbs))
		if(patient.getBruteLoss_nonProsthetic() > 0 || patient.getFireLoss_nonProsthetic() > 0)
			if(ishuman(patient))
				var/mob/living/carbon/human/H = patient
				var/list/blocked_spacesuit = list()
				var/list/blocked_armor = list()
				for(var/obj/item/bodypart/limb as anything in H.bodyparts)
					if(!limb.get_damage() && !has_treatable_wounds_on(limb))
						continue
					var/obj/item/clothing/covering = get_bodypart_protecting_clothing_by_coverage(H, limb)
					if(covering && (covering.clothing_flags & THICKMATERIAL))
						if(istype(covering, /obj/item/clothing/suit/space))
							blocked_spacesuit += ru_parse_zone(limb.body_zone)
						else
							blocked_armor += ru_parse_zone(limb.body_zone)
				if(length(blocked_spacesuit))
					patient.balloon_alert(user, "[blocked_spacesuit.Join(", ")] [length(blocked_spacesuit) > 1 ? "закрыты" : "закрыта"] скафандром!")
				else if(length(blocked_armor))
					patient.balloon_alert(user, "[blocked_armor.Join(", ")] [length(blocked_armor) > 1 ? "закрыты" : "закрыта"] бронёй!")
			return
		patient.balloon_alert(user, "полностью вылечен[patient.ru_a()]")
		return

	var/next_picked = (preferred_target in other_affected_limbs) ? preferred_target : other_affected_limbs[1]
	if(next_picked != last_zone)
		patient.balloon_alert(user, "переключаем на [ru_parse_zone(next_picked)]...")
	try_heal(patient, user, next_picked, silent = TRUE, auto_change_zone = TRUE, continuous = TRUE)

/obj/item/stack/medical/proc/try_heal_manual_target(mob/living/carbon/patient, mob/living/user)
	patient.balloon_alert(user, "оцениваем состояние...")
	if(!do_after(user, 1 SECONDS, patient))
		return
	var/new_zone = check_zone(user.zone_selected)
	if(!try_heal_checks(patient, user, new_zone))
		return
	patient.balloon_alert(user, "лечим [ru_parse_zone(new_zone)]...")
	try_heal(patient, user, new_zone, silent = TRUE, auto_change_zone = FALSE, continuous = TRUE)


/obj/item/stack/medical/proc/can_heal(mob/living/patient, mob/living/user, healed_zone, silent = FALSE)
	if(bypass_armor && ishuman(patient))
		var/mob/living/carbon/human/H = patient
		var/obj/item/bodypart/BP = H.get_bodypart(healed_zone)
		if(BP)
			var/obj/item/clothing/covering = get_bodypart_protecting_clothing_by_coverage(H, BP)
			if(covering && (covering.clothing_flags & THICKMATERIAL) && istype(covering, /obj/item/clothing/suit/space))
				if(!silent)
					patient.balloon_alert(user, "[ru_parse_zone(healed_zone)] закрыта скафандром!")
				return FALSE
		return patient.can_inject(user, !silent, healed_zone, TRUE)
	return patient.can_inject(user, !silent, healed_zone)

/obj/item/stack/medical/proc/has_healable_damage(mob/living/carbon/patient)
	if(heal_brute && patient.getBruteLoss_nonProsthetic() > 0)
		return TRUE
	if(heal_burn && patient.getFireLoss_nonProsthetic() > 0)
		return TRUE

	if((stop_bleeding || flesh_regeneration || sanitization) && LAZYLEN(patient.all_wounds))
		for(var/datum/wound/W as anything in patient.all_wounds)
			if(item_can_treat_wound(W))
				return TRUE

	return FALSE

/**
 * возвращает TRUE если этот предмет способен лечить данную рану.
 * Проверяет treatable_by и treatable_tool самой раны против типа и поведения предмета.
 */
/obj/item/stack/medical/proc/item_can_treat_wound(datum/wound/W)
	if(W.treatable_by)
		for(var/allowed_type in W.treatable_by)
			if(istype(src, allowed_type))
				return TRUE
	if(W.treatable_tool && tool_behaviour == W.treatable_tool)
		return TRUE
	return FALSE

/// возвращает TRUE если на данной конечности есть хотя бы одна рана, которую предмет умеет лечить
/obj/item/stack/medical/proc/has_treatable_wounds_on(obj/item/bodypart/affecting)
	for(var/datum/wound/W as anything in affecting.wounds)
		if(item_can_treat_wound(W))
			return TRUE
	return FALSE

/// Проверяет множество условий для определения возможности лечения пациента, включая can_heal
/// Даёт обратную связь если мы не можем вылечить пациента (если только silent не TRUE)
/obj/item/stack/medical/proc/try_heal_checks(mob/living/patient, mob/living/user, healed_zone, silent = FALSE)
	if(!can_heal(patient, user, healed_zone, silent))
		return FALSE

	if(!heal_dead && patient.stat == DEAD && !HAS_TRAIT(patient, TRAIT_UNDEAD))
		if(!silent)
			to_chat(user, "<span class='warning'>[patient] мертв[patient.ru_a()]! Вы не можете [patient.ru_emu()] помочь.</span>")
			patient.balloon_alert(user, "мёртв!")
		return FALSE

	if(iscarbon(patient))
		var/mob/living/carbon/carbon_patient = patient
		var/obj/item/bodypart/affecting = carbon_patient.get_bodypart(healed_zone)
		if(!affecting)
			if(!silent)
				to_chat(user, "<span class='warning'>У [patient] отсутствует \a [ru_parse_zone(healed_zone)]!</span>")
			return FALSE
		if(!affecting.is_organic_limb(FALSE))
			if(!silent)
				to_chat(user, "<span class='notice'>\The [src] не сработает для механической конечности!</span>")
			return FALSE

		var/can_heal_brute = heal_brute && affecting.brute_dam > 0
		var/can_heal_burn = heal_burn && affecting.burn_dam > 0
		// проверяем не просто наличие ран, а что предмет умеет лечить именно эти раны
		var/can_suture_bleeding = stop_bleeding && has_treatable_wounds_on(affecting)
		var/can_heal_burn_wounds = (flesh_regeneration || sanitization) && has_treatable_wounds_on(affecting)

		if(!can_heal_brute && !can_heal_burn && !can_heal_burn_wounds && !can_suture_bleeding)
			if(!silent)
				if(!can_heal_brute && stop_bleeding)
					to_chat(user, "<span class='notice'>[ru_kogo_zone(user.zone_selected)] [patient] не требует перевязки или лечения ушибов!</span>")
				else if(!can_heal_burn && (flesh_regeneration || sanitization))
					to_chat(user, "<span class='notice'>[ru_kogo_zone(user.zone_selected)] [patient] полностью обработан[patient.ru_a()], дайте время на заживление!</span>")
				else if(!affecting.brute_dam && !affecting.burn_dam)
					to_chat(user, "<span class='notice'>[ru_kogo_zone(user.zone_selected)] [patient] не повреждён[patient.ru_a()]!</span>")
				else
					to_chat(user, "<span class='notice'>[ru_kogo_zone(user.zone_selected)] [patient] нельзя вылечить при помощи \the [src].</span>")
			return FALSE
		return TRUE

	if(isanimal(patient))
		if(!heal_brute)
			if(!silent)
				to_chat(user, "<span class='warning'>Вы не можете вылечить [patient] при помощи \the [src]!</span>")
			return FALSE
		var/mob/living/simple_animal/critter = patient
		if(!critter.healable)
			if(!silent)
				to_chat(user, "<span class='notice'>Вы не можете применить \the [src] на [patient]!</span>")
			return FALSE
		if(critter.health == critter.maxHealth)
			if(!silent)
				to_chat(user, "<span class='notice'>[patient] полностью здоров[patient.ru_a()].</span>")
			return FALSE
		return TRUE

	return FALSE

/obj/item/stack/medical/proc/heal_carbon_new(mob/living/carbon/C, mob/user, healed_zone)
	var/efficiency = 1
	if(C.stat == DEAD)
		efficiency = heal_dead_multiplier

	var/obj/item/bodypart/affecting = C.get_bodypart(healed_zone)
	if(!affecting)
		return FALSE

	if(!affecting.is_organic_limb(FALSE))
		return FALSE

	var/healed_something = FALSE

	if((affecting.brute_dam && heal_brute) || (affecting.burn_dam && heal_burn))
		user.visible_message("<span class='green'>[user] наносит \the [src] на [ru_kogo_zone(affecting.name)] [C].</span>", "<span class='green'>Вы наносите \the [src] на [ru_kogo_zone(affecting.name)] [C].</span>")
		if(affecting.heal_damage(heal_brute*efficiency, heal_burn*efficiency))
			C.update_damage_overlays()
		healed_something = TRUE

	if(LAZYLEN(affecting.wounds))
		for(var/datum/wound/iter_wound as anything in affecting.wounds)
			if(stop_bleeding > 0 && (istype(iter_wound, /datum/wound/slash) || istype(iter_wound, /datum/wound/pierce)))
				iter_wound.blood_flow -= stop_bleeding * efficiency
				if(!healed_something)
					user.visible_message("<span class='green'>[user] обрабатывает раны на [ru_kogo_zone(affecting.name)] [C].</span>", "<span class='green'>Вы обрабатываете раны на [ru_kogo_zone(affecting.name)] [C].</span>")
				healed_something = TRUE

			if((flesh_regeneration > 0 || sanitization > 0) && istype(iter_wound, /datum/wound/burn))
				var/datum/wound/burn/burn_wound = iter_wound
				if(flesh_regeneration > 0)
					burn_wound.flesh_healing += flesh_regeneration * efficiency
				if(sanitization > 0)
					burn_wound.sanitization += sanitization * efficiency
				if(!healed_something)
					user.visible_message("<span class='green'>[user] обрабатывает раны на [ru_kogo_zone(affecting.name)] [C].</span>", "<span class='green'>Вы обрабатываете раны на [ru_kogo_zone(affecting.name)] [C].</span>")
				healed_something = TRUE

	return healed_something

/obj/item/stack/medical/proc/heal_animal(mob/living/simple_animal/M, mob/user)
	var/efficiency = 1
	if(M.stat == DEAD)
		if(!heal_dead)
			return FALSE
		efficiency = heal_dead_multiplier

	if(!M.healable)
		return FALSE
	if(M.health == M.maxHealth)
		return FALSE

	user.visible_message("<span class='green'>[user] наносит \the [src] на [M].</span>", "<span class='green'>Вы наносите \the [src] на [M].</span>")
	if(AmBloodsucker(M))
		return TRUE
	M.heal_bodypart_damage((heal_brute/2)*efficiency)
	return TRUE

/obj/item/stack/medical/proc/heal(mob/living/M, mob/user)
	return

/obj/item/stack/medical/get_belt_overlay()
	return mutable_appearance('icons/obj/clothing/belt_overlays.dmi', "pouch")

/obj/item/stack/medical/bruise_pack
	name = "bruise pack"
	singular_name = "bruise pack"
	desc = "Терапевтическая упаковка геля и повязок для работы с травмами от тупых предметов."
	icon_state = "brutepack"
	lefthand_file = 'icons/mob/inhands/equipment/medical_lefthand.dmi'
	righthand_file = 'icons/mob/inhands/equipment/medical_righthand.dmi'
	heal_brute = 40
	self_delay = 40
	other_delay = 20
	grind_results = list(/datum/reagent/medicine/styptic_powder = 10)

/obj/item/stack/medical/bruise_pack/one
	amount = 1

/obj/item/stack/medical/bruise_pack/suicide_act(mob/user)
	user.visible_message("<span class='suicide'>[user] is bludgeoning себя with [src]! It looks like [user.p_theyre()] trying to commit suicide!</span>")
	return (BRUTELOSS)

/obj/item/stack/medical/gauze
	name = "medical gauze"
	desc = "Моток эластичной ткани, идеальной для стабилизации любых видов ранений, от порезов до ожогов и переломов костей."
	gender = PLURAL
	singular_name = "medical gauze"
	icon_state = "gauze"
	heal_brute = 5
	heal_burn = 5
	self_delay = 50
	other_delay = 20
	amount = 15
	max_amount = 15
	absorption_rate = 0.25
	absorption_capacity = 5
	splint_factor = 0.35
	custom_price = PRICE_REALLY_CHEAP
	grind_results = list(/datum/reagent/cellulose = 2)

/obj/item/stack/medical/gauze/has_healable_damage(mob/living/carbon/patient)
	if(..())
		return TRUE
	for(var/obj/item/bodypart/limb as anything in patient.bodyparts)
		for(var/datum/wound/W as anything in limb.wounds)
			if(W.wound_flags & ACCEPTS_GAUZE)
				return TRUE
	return FALSE

/obj/item/stack/medical/gauze/try_heal_checks(mob/living/patient, mob/living/user, healed_zone, silent = FALSE)
	if(!can_heal(patient, user, healed_zone, silent))
		return FALSE
	if(!heal_dead && patient.stat == DEAD && !HAS_TRAIT(patient, TRAIT_UNDEAD))
		if(!silent)
			to_chat(user, "<span class='warning'>[patient] мёртв[patient.ru_a()]! Вы не можете [patient.ru_emu()] помочь.</span>")
			patient.balloon_alert(user, "мёртв[patient.ru_a()]!")
		return FALSE
	if(!iscarbon(patient))
		return FALSE
	var/mob/living/carbon/carbon_patient = patient
	var/obj/item/bodypart/affecting = carbon_patient.get_bodypart(healed_zone)
	if(!affecting)
		if(!silent)
			to_chat(user, "<span class='warning'>У [patient] отсутствует \a [ru_parse_zone(healed_zone)]!</span>")
		return FALSE
	if(heal_brute && affecting.brute_dam > 0)
		return TRUE
	if(heal_burn && affecting.burn_dam > 0)
		return TRUE
	for(var/datum/wound/W as anything in affecting.wounds)
		if(W.wound_flags & ACCEPTS_GAUZE)
			return TRUE
	if(!silent)
		to_chat(user, "<span class='notice'>[ru_kogo_zone(user.zone_selected)] [patient] не требует перевязки!</span>")
	return FALSE

// Марля актуальна только для ран, которые обрабатываются самими ранами
/obj/item/stack/medical/gauze/try_heal(mob/living/M, mob/user, healed_zone, silent = FALSE, auto_change_zone = TRUE, continuous = FALSE)
	var/obj/item/bodypart/limb = M.get_bodypart(healed_zone)
	if(!limb)
		to_chat(user, "<span class='notice'>Нечего перевязывать!</span>")
		return

	var/gauzeable_wound = FALSE
	for(var/datum/wound/woundies as anything in limb.wounds)
		if(woundies.wound_flags & ACCEPTS_GAUZE)
			gauzeable_wound = TRUE
			break
	if(!gauzeable_wound)
		return ..()

	if(limb.current_gauze && (limb.current_gauze.absorption_capacity * 0.8 > absorption_capacity)) // игнорируем если новая повязка меньше чем на 20% лучше текущей, чтобы кто-то не перевязывал её 5 раз подряд
		to_chat(user, "<span class='warning'>Повязка, что наложена на [user==M ? "вашей [limb.ru_name_v]" : "[limb.ru_name_v] персонажа[M]"], пока ещё хорошем состоянии!</span>")
		return

	user.visible_message("<span class='warning'>[user] пытается перевязать рану на [limb.ru_name_v] персонажа [M] с помощью [src]...</span>", "<span class='warning'>Вы пытаетесь перевязать раны на [user == M ? "вашей [limb.ru_name_v]" : "[limb.ru_name_v] персонажа [M]"] с помощью [src]...</span>")

	if(!do_after(user, (user == M ? self_delay : other_delay), target=M))
		return

	user.visible_message("<span class='green'>[user] наносит [src] на конечность персонажа [M]</span>", "<span class='green'>Вы пытаетесь перевязать раны на [user == M ? "своей конечности" : "конечности персонажа [M]"].</span>")
	limb.apply_gauze(src)
	if((heal_brute && limb.brute_dam > 0) || (heal_burn && limb.burn_dam > 0))
		heal_carbon_new(M, user, healed_zone)

/obj/item/stack/medical/gauze/attackby(obj/item/I, mob/user, params)
	if(I.tool_behaviour == TOOL_WIRECUTTER || I.get_sharpness())
		if(get_amount() < 2)
			to_chat(user, "<span class='warning'>Вам необходимо как минимум две марлевых повязки!</span>")
			return
		new /obj/item/stack/sheet/cloth(user.drop_location())
		user.visible_message("[user] разрезает [src] на части с помощью [I].", \
					 "<span class='notice'>Вы разрезаете [src] на части с помощью [I].</span>", \
					 "<span class='italics'>Вы слышите звук разрезания ткани.</span>")
		use(2)
	else if(I.is_drainable() && I.reagents.has_reagent(/datum/reagent/space_cleaner/sterilizine))
		if(!I.reagents.has_reagent(/datum/reagent/space_cleaner/sterilizine, 5))
			to_chat(user, "<span class='warning'>Не хватает стерилизина в [I], чтобы обработать [src]!</span>")
			return
		user.visible_message("<span class='notice'>[user] обрабатывает [src] с помощью содержимого [I].</span>", "<span class='notice'>Вы выливаете содержимое [I] на [src], обрабатывая это.</span>")
		I.reagents.remove_reagent(/datum/reagent/space_cleaner/sterilizine, 5)
		new /obj/item/stack/medical/gauze/adv/one(user.drop_location())
		use(1)
	else
		return ..()

/obj/item/stack/medical/gauze/suicide_act(mob/living/user)
	user.visible_message("<span class='suicide'>[user] пытается обвязать \the [src] вокруг [user.ru_ego()] шеи! Похоже, [user.ru_who()] не совсем понимает, как пользоваться медикаментами!</span>")
	return OXYLOSS

/obj/item/stack/medical/gauze/one
	amount = 1

/obj/item/stack/medical/gauze/improvised
	name = "improvised gauze"
	singular_name = "improvised gauze"
	heal_brute = 0
	desc = "Моток грубо обрезанной ткани от чего-то делавшего хорошую работу в стабилизации ран. Делает это не так хорошо, чем полноценная повязка."
	self_delay = 60
	other_delay = 30
	absorption_rate = 0.15
	absorption_capacity = 4
	splint_factor = 0.15

/obj/item/stack/medical/gauze/adv
	name = "sterilized medical gauze"
	singular_name = "sterilized medical gauze"
	desc = "Моток эластичной стерилизованной ткани. Экстремально эффективна для остановки кровотечений и стабилизации ожогов."
	heal_brute = 7
	self_delay = 45
	other_delay = 15
	absorption_rate = 0.5
	absorption_capacity = 12
	splint_factor = 0.5

/obj/item/stack/medical/gauze/adv/one
	amount = 1

/obj/item/stack/medical/gauze/cyborg
	custom_materials = null
	is_cyborg = TRUE
	source = /datum/robot_energy_storage/medical
	cost = 250

/obj/item/stack/medical/suture
	name = "suture"
	desc = "Стандартная стерилизованная нить для закрытия порезов, рваных ран и остановок кровотечения."
	gender = PLURAL
	singular_name = "suture"
	icon_state = "suture"
	self_delay = 30
	other_delay = 10
	amount = 15
	max_amount = 15
	repeating = TRUE
	heal_brute = 13
	stop_bleeding = 0.6
	grind_results = list(/datum/reagent/medicine/spaceacillin = 2)

/obj/item/stack/medical/suture/emergency
	name = "emergency suture"
	desc = "Моток дешёвой нити, не очень хорошей для латания ран, но неплохо подходящей против кровотечений."
	heal_brute = 10
	amount = 5
	max_amount = 5

/obj/item/stack/medical/suture/one
	amount = 1

/obj/item/stack/medical/suture/five
	amount = 5

/obj/item/stack/medical/suture/medicated
	name = "medicated suture"
	icon_state = "suture_purp"
	desc = "Нить, смоченная в лекарствах, помогающих в заживлении самых тяжёлых рваных ран."
	heal_brute = 20
	stop_bleeding = 1
	grind_results = list(/datum/reagent/medicine/polypyr = 2)
	heal_dead = TRUE
	heal_dead_multiplier = 0.65
	bypass_armor = TRUE // Лечит сковозь броню.

/obj/item/stack/medical/suture/medicated/one
	amount = 1

/obj/item/stack/medical/suture/one
	amount = 1

/obj/item/stack/medical/ointment
	name = "ointment"
	desc = "Стандартная мазь от ожогов, вполне эфффективная против ожогов второй степени при бинтовании, впрочем, также стабилизирует и более серьёзные ожоги. Не прям хороша для полного заживления ожогов."
	gender = PLURAL
	singular_name = "ointment"
	icon_state = "ointment"
	lefthand_file = 'icons/mob/inhands/equipment/medical_lefthand.dmi'
	righthand_file = 'icons/mob/inhands/equipment/medical_righthand.dmi'
	amount = 12
	max_amount = 12
	self_delay = 40
	other_delay = 20

	heal_burn = 10
	flesh_regeneration = 2.5
	sanitization = 0.4
	grind_results = list(/datum/reagent/medicine/kelotane = 10)

/obj/item/stack/medical/ointment/suicide_act(mob/living/user)
	user.visible_message("<span class='suicide'>[user] выдавливает \the [src] в свой рот! Он[user.ru_a()] вообще знает, что оно ядовито?!</span>")
	return TOXLOSS

/obj/item/stack/medical/mesh
	name = "regenerative mesh"
	desc = "Бактерицидная сетка для оборачивания ожогов."
	gender = PLURAL
	singular_name = "regenerative mesh"
	icon_state = "regen_mesh"
	self_delay = 30
	other_delay = 10
	amount = 15
	max_amount = 15
	heal_burn = 13
	repeating = TRUE
	sanitization = 0.75
	flesh_regeneration = 3
	var/is_open = TRUE /// Эта переменная определяет, была ли открыта стерильная упаковка сетки.
	grind_results = list(/datum/reagent/medicine/spaceacillin = 2)

/obj/item/stack/medical/mesh/one
	amount = 1

/obj/item/stack/medical/mesh/five
	amount = 5

/obj/item/stack/medical/mesh/advanced
	name = "advanced regenerative mesh"
	desc = "Продвинутая стека со смесью экстракта алоэ и стрелизирующих агентов, для работы с ожогами."
	gender = PLURAL
	singular_name = "advanced regenerative mesh"
	icon_state = "aloe_mesh"
	heal_burn = 20
	sanitization = 1.25
	flesh_regeneration = 5
	grind_results = list(/datum/reagent/consumable/aloejuice = 1)
	heal_dead = TRUE
	heal_dead_multiplier = 0.65
	bypass_armor = TRUE // Лечит сквозь броню.

/obj/item/stack/medical/mesh/advanced/one
	amount = 1

/obj/item/stack/medical/mesh/Initialize(mapload)
	. = ..()
	if(amount == max_amount)	 // запечатываем только полные упаковки сетки
		is_open = FALSE
		update_icon()

/obj/item/stack/medical/mesh/advanced/update_icon_state()
	if(!is_open)
		icon_state = "aloe_mesh_closed"
	else
		return ..()

/obj/item/stack/medical/mesh/update_icon_state()
	if(!is_open)
		icon_state = "regen_mesh_closed"
	else
		return ..()

/obj/item/stack/medical/mesh/try_heal_checks(mob/living/patient, mob/living/user, healed_zone, silent = FALSE)
	if(!is_open)
		if(!silent)
			to_chat(user, "<span class='warning'>Вам нужно для начала раскрыть [src].</span>")
		return FALSE
	return ..()

/obj/item/stack/medical/mesh/AltClick(mob/living/user)
	if(!is_open)
		to_chat(user, "<span class='warning'>Вам нужно для начала раскрыть [src].</span>")
		return
	. = ..()

/obj/item/stack/medical/mesh/on_attack_hand(mob/user, act_intent = user.a_intent, unarmed_attack_flags)
	if(!is_open && (user.get_inactive_held_item() == src))
		to_chat(user, "<span class='warning'>Вам нужно для начала раскрыть [src].</span>")
		return
	. = ..()

/obj/item/stack/medical/mesh/attack_self(mob/user)
	if(!is_open)
		is_open = TRUE
		to_chat(user, "<span class='notice'>Вы раскрыли упакопку стерильной сетки.</span>")
		update_icon()
		playsound(src, 'sound/items/poster_ripped.ogg', 20, TRUE)
		return
	. = ..()

/obj/item/stack/medical/bone_gel
	name = "bone gel"
	singular_name = "bone gel"
	desc = "Сильнодействующий медицинский гель, при правильном применении на повреждённую кость провоцирует интенсивную реакцию сращивания костных тканей. Может быть применён напрямую, как и хирургическая лента, напрямую на кость в крайнем случае, что, впрочем, очень вредно пациенту и не рекомендуется."

	icon = 'icons/obj/surgery.dmi'
	icon_state = "bone-gel"
	lefthand_file = 'icons/mob/inhands/equipment/medical_lefthand.dmi'
	righthand_file = 'icons/mob/inhands/equipment/medical_righthand.dmi'

	amount = 4
	self_delay = 20
	grind_results = list(/datum/reagent/medicine/bicaridine = 10)
	novariants = TRUE

/obj/item/stack/medical/bone_gel/attack(mob/living/M, mob/user)
	to_chat(user, "<span class='warning'>Костный гель может быть применён только на раздробленные конечности в [span_red("агрессивном")] хвате!</span>")
	return

/obj/item/stack/medical/bone_gel/suicide_act(mob/user)
	if(iscarbon(user))
		var/mob/living/carbon/C = user
		C.visible_message("<span class='suicide'>[C] выдавливает весь \the [src] внутрь своего рта! Это не правильное применение! Похоже, что [C.ru_who()] пытается совершить суицид!</span>")
		if(do_after(C, 2 SECONDS))
			C.emote("realagony")
			for(var/i in C.bodyparts)
				var/obj/item/bodypart/bone = i
				var/datum/wound/blunt/severe/oof_ouch = new
				oof_ouch.apply_wound(bone)
				var/datum/wound/blunt/critical/oof_OUCH = new
				oof_OUCH.apply_wound(bone)

			for(var/i in C.bodyparts)
				var/obj/item/bodypart/bone = i
				bone.receive_damage(brute=60)
			use(1)
			return (BRUTELOSS)
		else
			C.visible_message("<span class='suicide'>[C] проваливает затею как идиот и всё равно умудряется сдохнуть!</span>")
			return (BRUTELOSS)

/obj/item/stack/medical/bone_gel/cyborg
	custom_materials = null
	is_cyborg = TRUE
	source = /datum/robot_energy_storage/medical
	cost = 250

/obj/item/stack/medical/aloe
	name = "aloe cream"
	desc = "Лечащая паста для открытых ран."

	icon_state = "aloe_paste"
	self_delay = 20
	other_delay = 10
	novariants = TRUE
	amount = 20
	max_amount = 20
	var/heal = 3
	heal_brute = 3
	heal_burn = 3
	grind_results = list(/datum/reagent/consumable/aloejuice = 1)

/obj/item/stack/medical/aloe/fresh
	amount = 2

/obj/item/stack/medical/nanogel
	name = "nanogel"
	singular_name = "nanogel"
	desc = "Высокотехнологичный гель, при применении на отремонтированную снаружи роботическую конечность - нейтрализует остаточные внутренние повреждения, позволяя дальнейшее обслуживание без хирургии."
	self_delay = 150	// Мучительно медленно при использовании на себе, но не полностью запрещено, потому что антагонистам с роболимбами нужен способ справляться с порогами.
	other_delay = 30	// Довольно быстро при использовании на других.
	amount = 12
	max_amount = 12	// Две синтетические конечности стоит починки, если каждая часть тела имеет внутренние повреждения. Обычно, вероятно, больше 6-12.
	icon_state = "nanogel"
	var/being_applied = FALSE	// Запрет на накопление doafter.

/obj/item/stack/medical/nanogel/one
	amount = 1

/obj/item/stack/medical/nanogel/try_heal_checks(mob/living/patient, mob/living/user, healed_zone, silent = FALSE)
	if(!iscarbon(patient))
		if(!silent)
			to_chat(user, "<span class='warning'>Оно не поможет [patient]!</span>")
		return FALSE
	return ..()

/obj/item/stack/medical/nanogel/heal_carbon_new(mob/living/carbon/C, mob/user, healed_zone)
	if(!C)
		return FALSE
	var/obj/item/bodypart/affecting = C.get_bodypart(healed_zone)
	if(!affecting) // Отсутствует конечность?
		to_chat(user, "<span class='warning'>[C] не имеет \a [ru_parse_zone(healed_zone)]!</span>")
		return FALSE
	if(!affecting.is_robotic_limb())
		to_chat(user, "<span class='warning'>Это не поможет нероботическим конечностям!</span>")
		return FALSE
	if(!affecting.threshhold_brute_passed && !affecting.threshhold_burn_passed)
		to_chat(user, "<span class='warning'>Нет нужды намазывать гель на [affecting].</span>")
		return FALSE
	if(affecting.threshhold_brute_passed && affecting.brute_dam == affecting.threshhold_passed_mindamage)
		. = TRUE
		affecting.threshhold_brute_passed = FALSE
	if(affecting.threshhold_burn_passed && affecting.burn_dam == affecting.threshhold_passed_mindamage)
		. = TRUE
		affecting.threshhold_burn_passed = FALSE
	if(.)
		user.visible_message("<span class='green'>Наногель вступает в реакцию на теле [C], ремонтируя внутренние повреждения [affecting].</span>", "<span class='green'>Вы наблюдаете как наногель начинает работу по ремонту внутренних повреждений [affecting]</span>")
		return TRUE
	// Если дошли сюда: Провал, давайте скажем пользователю почему.
	to_chat(user, "<span class='warning'>[src] терпит неудачу в с [affecting] из-за остаточного урона [(affecting.threshhold_brute_passed && affecting.threshhold_burn_passed) ? "травм и ожогов" : "[affecting.threshhold_burn_passed ? "ожогами" : "травмами"]"]! Проведите внешне обслуживание перед применением.</span>")
	return FALSE
