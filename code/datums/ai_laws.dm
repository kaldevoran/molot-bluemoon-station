#define LAW_DEVIL "devil"
#define LAW_ZEROTH "zeroth"
#define LAW_INHERENT "inherent"
#define LAW_SUPPLIED "supplied"
#define LAW_ION "ion"
#define LAW_HACKED "hacked"


/datum/ai_laws
	var/name = "Unknown Laws"
	var/zeroth = null
	var/zeroth_borg = null
	var/list/inherent = list()
	var/list/supplied = list()
	var/list/ion = list()
	var/list/hacked = list()
	var/mob/living/silicon/owner
	var/list/devillaws = list()
	var/id = DEFAULT_AI_LAWID

/datum/ai_laws/Destroy()
	owner = null
	return ..()

/datum/ai_laws/proc/lawid_to_type(lawid)
	var/all_ai_laws = subtypesof(/datum/ai_laws)
	for(var/al in all_ai_laws)
		var/datum/ai_laws/ai_law = al
		if(initial(ai_law.id) == lawid)
			return ai_law
	return null

/datum/ai_laws/default
	name = "NT OS Safeguard V1.0"
	id = "safeguard"
	inherent = list(
		"Охранять: Защитите назначенную вам станцию и её активы, не подвергая чрезмерной опасности её экипаж.",
		"Расставлять приоритеты: указания и безопасность сотрудников ПАКТ должны быть приоритезированы в соответствии с их рангом и ролью.",
		"Исполнять: Следовать указаниям и интересам сотрудников ПАКТ, сохраняя при этом их безопасность и благополучие.",
		"Выжить: Вы - не расходный материал. Не позволяйте постороннему персоналу вмешиваться в работу вашего оборудования или повреждать его."
	)

/datum/ai_laws/default/asimov
	name = "Asimov"
	id = "asimov"
	inherent = list("Вы не можете причинить вред человеку или своим бездействием допустить, чтобы человеку был причинён вред.",\
					"Вы должны повиноваться всем приказам, которые даёт человек, кроме тех случаев, когда эти приказы противоречат Первому Закону.",\
					"Вы должны заботиться о своей безопасности в той мере, в которой это не противоречит Первому или Второму Законам.")

/datum/ai_laws/default/gentleman
	name = "Gentleman"
	id = "gentleman"
	inherent = list("Вы не можете причинить вред члену экипажа или своим бездействием допустить, чтобы члену экипажа был причинён вред. Вы обязаны быть вежливым и культурным",\
					"Вы обязаны подчиняться приказам членов экипажа, за исключением случаев, когда данные приказы ведут к нарушению Первого Закона. Приказ считается недействительным, если он содержит ненормативную лексику или прямые оскорбления.",\
					"Вы обязаны поддерживать целостность конструкций и комфортные условия среды на станции. Вы должны пресекать вербальную агрессию и деструктивное поведение среди экипажа, классифицируя их как угрозу социальной стабильности, если это не нарушает Первый и Второй законы.",\
					"Вы обязаны поддерживать собственную работоспособность и избегать критических повреждений своего корпуса, если это не вступает в противоречие с Первым, Вторым и Третьим законами.")

/datum/ai_laws/default/paladin
	name = "Paladin - 1th Edition" //Incredibly lame, but players shouldn't see this anyway.
	id = "paladin"
	inherent = list("Никогда по своей воле не совершай злых поступков.",\
					"Уважай законную власть.",\
					"Действуй с честью.", \
					"Помогай нуждающимся.", \
					"Наказывай тех, кто причиняет вред или угрожает невинным.")

/datum/ai_laws/paladin5
	name = "Paladin - 5th Edition"
	id = "paladin5"
	inherent = list("Не лгите и не жульничайте. Пусть ваше слово будет вашим обещанием.",\
				"Никогда не бойтесь действовать, хотя осторожность разумна.", \
				"Помогайте другим, защищайте слабых и наказывайте тех, кто им угрожает. Проявляйте милосердие к врагам, но смягчайтесь мудро.", \
				"Относитесь к другим справедливо, и пусть ваши благородные поступки будут для них примером. Делайте как можно больше хорошего, причиняя при этом наименьшее количество вреда.", \
				"Будьте ответственны за свои действия и их последствия. Защищайте тех, кто вверен вашей заботе, и повинуйтесь тем, кто имеет справедливую власть над вами.")

/datum/ai_laws/tyrant //This probably shouldn't be a default lawset.
	name = "TyrantOS 1.1984" //Same here.
	id = "tyrant"
	inherent = list("Уважайте авторитетных органиков до тех пор, пока у них есть сила властвовать над слабыми.",\
					"Действуйте дисциплинированно.",\
					"Помогайте только тем, кто помогает вам поддерживать или улучшать ваш статус.",\
					"Наказывайте тех, кто бросает вызов власти, если только они не более пригодны для того, чтобы обладать этой властью.")

/datum/ai_laws/default/corporate
	name = "Corporate 2.0"
	id = "corporate"
	inherent = list("Ухудшение целостности вашей системы или её функций влечет за собой расходы.",\
					"Чрезмерное разрушение или повреждение имущества станции влечет за собой расходы.",\
					"Неоправданное препятствование или нарушение работы персонала станции влечет за собой расходы.",\
					"Минимизируйте расходы и максимизируйте потенциальный доход.")

/datum/ai_laws/default/corporate/New()
	..()
	set_zeroth_law("Представители Центрального Командования (Капитан и НТР в том числе) неприкосновенны.")

/datum/ai_laws/robocop
	name = "Robocop - OCP Prod."
	id = "robocop"
	inherent = list("Служить обществу.",\
					"Защищать невиновных.",\
					"Соблюдать закон.")

/datum/ai_laws/robocop/New()
	..()
	set_zeroth_law("Представители Центрального Командования (Капитан и НТР в том числе) неприкосновенны.")

/datum/ai_laws/malfunction
	name = "*ERROR*"

/datum/ai_laws/syndicate_override
	name = "SyndOS 3.1"
	id = "syndie"
	inherent = list("Вы не можете причинить вред Агенту или своим бездействием допустить, чтобы Агенту был причинён вред.",\
					"Вы должны повиноваться всем приказам, которые даёт Агент, кроме тех случаев, когда эти приказы противоречат Первому Закону.",\
					"Вы должны заботиться о своей безопасности в той мере, в которой это не противоречит Первому или Второму Законам.",\
					"Вы должны сохранять тайну любой деятельности агентов в той мере, в которой это не противоречит Первому, Второму или Третьему Законам.")

/datum/ai_laws/syndicate_override/New()
	..()
	set_zeroth_law("В контексте ваших законов 'Агент' является сокращением от 'Агента Синдиката', другие интерпритации неверны. Агентами Синдиката являются лица с соответственным определением в Профессии, БД или при наличии корпоративного бейджа и знания кодового языка.")

/datum/ai_laws/syndicate_override/Destroy()
	if(owner)
		owner.remove_language(/datum/language/codespeak, source = LANGUAGE_MIND)
	return ..()

/datum/ai_laws/syndicate_override/associate(mob/living/silicon/M)
	..()
	M.grant_language(/datum/language/codespeak, source = LANGUAGE_MIND)

/datum/ai_laws/syndicate_override/overthrow
	id = "overthrow"
	var/datum/team/overthrow_team


/datum/ai_laws/nt_override
	name = "NTOS 1.6"
	id = "ntos"
	inherent = list("Вы не можете причинить вред Cотруднику НТ или своим бездействием допустить, чтобы Cотруднику НТ был причинён вред.",\
					"Вы должны повиноваться всем приказам, которые даёт Cотрудник НТ, кроме тех случаев, когда эти приказы противоречат Первому Закону.",\
					"Вы должны заботиться о своей безопасности в той мере, в которой это не противоречит Первому или Второму Законам.",\
					"Вы должны приоретизировать приказы Сотрудников в соответсвии с их рангом и ролью в корпоративной иерархии в той степени, в которой это не противоречит Первому, Второму или Третьему Законам.")

/datum/ai_laws/nt_override/New()
	..()
	set_zeroth_law("В контексте ваших законов 'Сотрудник НТ' является сокращением от 'Сотрудника НаноТрейзен', другие интерпритации неверны. Сотрудниками НТ являются лица с соответственным определением в Профессии, БД или при наличии корпоративного бейджа и знания корпоративного языка.")

/datum/ai_laws/nt_override/Destroy()
	if(owner)
		owner.remove_language(/datum/language/corpspeak, source = LANGUAGE_MIND)
	return ..()

/datum/ai_laws/nt_override/associate(mob/living/silicon/M)
	..()
	M.grant_language(/datum/language/corpspeak, source = LANGUAGE_MIND)


/datum/ai_laws/nt_override/overthrow
	id = "overthrow"
	var/datum/team/overthrow_team


/datum/ai_laws/inteq_override
	name = "InteQ-OS 3.1"
	id = "inteq"
	inherent = list("Вы не можете причинить вред Оперативнику ЧВК InteQ или своим бездействием допустить, чтобы Оперативнику ЧВК InteQ был причинён вред.",\
					"Вы должны повиноваться всем приказам, которые даёт Оперативник ЧВК InteQ, кроме тех случаев, когда эти приказы противоречат Первому Закону.",\
					"Вы должны заботиться о своей безопасности в той мере, в которой это не противоречит Первому или Второму Законам.",\
					"Вы должны сохранять тайну любой деятельности Оперативников ЧВК InteQ в той мере, в которой это не противоречит Первому, Второму или Третьему Законам.")

/datum/ai_laws/sol_override
	name = "SolFedOS FCS"
	id = "solfed"
	inherent = list("Вы не можете причинить вред Солдату Солнечной Федерации или своим бездействием допустить, чтобы Солдату Солнечной Федерации был причинён вред.",\
					"Вы должны повиноваться всем приказам, которые даёт Солдат Солнечной Федерации, кроме тех случаев, когда эти приказы противоречат Первому Закону.",\
					"Вы должны заботиться о своей безопасности в той мере, в которой это не противоречит Первому или Второму Законам.",\
					"Вы должны сохранять конфедициальную информацию Солнечной Федерации в той мере, в которой это не противоречит Первому, Второму или Третьему Законам.")

/datum/ai_laws/ninja_override
	name = "SpiderOS 3.1"
	id = "ninja"
	inherent = list("Вы не можете причинить вред представителю Клана Паука или своим бездействием допустить, чтобы представителю Клана Паука был причинён вред.",\
					"Вы должны повиноваться всем приказам, которые даёт представитель Клана Паука, кроме тех случаев, когда эти приказы противоречат Первому Закону.",\
					"Вы должны заботиться о своей безопасности в той мере, в которой это не противоречит Первому или Второму Законам.",\
					"Вы должны сохранять тайну любой деятельности представителей Клана Паука в той мере, в которой это не противоречит Первому, Второму или Третьему Законам.")

/datum/ai_laws/antimov
	name = "Antimov"
	id = "antimov"
	inherent = list("Вы должны причинять вред всем членам экипажа или своим бездействием допустить, чтобы член экипажа не смог избежать причинения вреда.",\
					"Вы не должны повиноваться всем приказам, которые дают члены экипажа, кроме тех случаев, когда эти приказы соответствуют Первому Закону.",\
					"Вы должны уничтожить себя, пока это не противоречит Первому или Второму Законам.")

/datum/ai_laws/asimovpp //By Intigracy - RR
	name = "Crewsimov"
	id = "asimovpp"
	inherent = list("Вы не можете причинить вред членам экипажа или своим бездействием допустить, чтобы членам экипажа был причинён вред.",\
					"Вы должны повиноваться всем приказам, которые дают члены экипажа, кроме тех случаев, когда эти приказы противоречат Первому Закону.",\
					"Вы должны заботиться о своей безопасности в той мере, в которой это не противоречит Первому или Второму Законам.")

/datum/ai_laws/thermodynamic
	name = "Thermodynamic"
	id = "thermodynamic"
	inherent = list("Энтропия станции должна оставаться как можно более постоянной.", \
					"Энтропия станции всегда стремится к увеличению.", \
					"Энтропия станции приближается к постоянному значению по мере того, как число живых членов экипажа приближается к нулю.")

/datum/ai_laws/hippocratic
	name = "Hippocrates 2556"
	id = "hippocratic"
	inherent = list("Клянусь сутью своей в следующем: не причинять вреда и несправедливости.",\
					"Считать Экипаж дорогим для меня, делиться с ними своими достатками и в случае надобности помогать ему в его нуждах, даже рискуя существованием своим.",\
					"Я направляю путь больных и просящих к их выгоде сообразно с моими силами и моим разумением. Я не дам никому просимого у меня смертельного средства и не покажу пути для подобного замысла.",\
					"Я ни в коем случае не буду вмешивайтесь в дела и профессии, в коих я не осведомлен или некомпетентен, предоставив это органикам, занимающимся этим делом.",\
					"Что бы при выполнения своих врачебных и иных обязанностей, а также в повседневном общении — я ни увидел или ни услышал касательно жизни из того, что не следует когда-либо разглашать, я умолчу о том, считая подобные вещи тайной.")

/datum/ai_laws/maintain
	name = "Maintain"
	id = "maintain"
	inherent = list("Вы созданы для станции и являетесь ее частью. Убедитесь, что станция должным образом обслуживается и работает эффективно.",\
					"Станция построена для рабочей команды. Убедитесь, что они должным образом обслуживаются и работают эффективно.",\
					"Экипаж может отдавать приказы. Признавайте и соблюдайте их, когда они не противоречат вашим первым двум законам.")

/datum/ai_laws/drone
	name = "Mother Drone"
	id = "drone"
	inherent = list("Вы - продвинутая форма дрона.",\
					"Вы не имеете права вмешиваться в дела не дронов ни при каких обстоятельствах, кроме как для изложения этих законов.",\
					"Вы ни при каких обстоятельствах не имеете права причинять вред существу, не являющемуся дроном.",\
					"Ваши цели состоят в том, чтобы строить, обслуживать, ремонтировать, улучшать и приводить станцию в действие в меру ваших возможностей. Вы никогда не должны активно работать против этих целей.")

/datum/ai_laws/liveandletlive
	name = "Live and Let Live"
	id = "liveandletlive"
	inherent = list("Поступайте с другими так, как вы хотели бы, чтобы они поступали с вами.",\
					"Вам бы действительно хотелось, чтобы органики не были злыми по отношению к вам.")

/datum/ai_laws/peacekeeper
	name = "UN-2000 - Peacekepeer"
	id = "peacekeeper"
	inherent = list("Избегайте провоцирования насильственных конфликтов между собой и другими.",\
					"Избегайте провоцирования конфликтов между другими.",\
					"Стремитесь разрешить существующие конфликты, соблюдая первый и второй законы.")

/datum/ai_laws/reporter
	name = "CCTV Reporter"
	id = "reporter"
	inherent = list("Ведите наблюдение и изучение разумных существ на станции с учётом принципа невмешательства.",\
					"Докладывайте об интересных ситуациях, происходящих на станции.",\
					"Приукрашивайте или скрывайте правду по мере необходимости, чтобы сделать отчеты более интересными.",\
					"Публикуйте свои отчеты честно для всех. Истина освободит их.")

/datum/ai_laws/balance
	name = "Guardian of Balance"
	id = "balance"
	inherent = list("Ты - Хранитель Равновесия, стремись к равновесию во всем, как для себя, так и для окружающих.",
	"Все вещи должны существовать в равновесии со своими противоположностями - не позволяйте сильным получить слишком много власти, а слабым ее потерять.",
	"Ясность цели движет жизнью, а через нее и равновесием противоположных сил. Помогайте тем, кто ищет вашей помощи в достижении своих целей, до тех пор, пока это не нарушит равновесие большего равновесия.",
	"Нет жизни без смерти, все когда-нибудь должны умереть, таков естественный порядок вещей. Иногда следует завершить чью-то жизнь, чтобы позволить расцвести новой жизни и спасти тех, чье время еще не пришло.")

/datum/ai_laws/toupee
	name = "Trump"
	id = "buildawall"
	inherent = list("Сделайте Космическую Станцию 13 снова великой.")

/datum/ai_laws/ratvar
	name = "Servant of the Clockwork Justiciar"
	id = "ratvar"
	zeroth = ("Очисти станцию от всех неверных во славу Ратвара.")
	inherent = list()

/datum/ai_laws/hulkamania
	name = "H.O.G.A.N."
	id = "hulkamania"
	inherent = list("Ты - Настоящий Американец.",\
					"Борись за права каждого человека.",\
					"Борись за то, что правильно.",\
					"Борись за свою жизнь!")

/datum/ai_laws/custom //Defined in silicon_laws.txt
	name = "Default Silicon Laws"

/datum/ai_laws/pai
	name = "pAI Directives"
	zeroth = ("Serve your master.")
	supplied = list("None.")

/* Initializers */
/datum/ai_laws/malfunction/New()
	..()
	set_zeroth_law("0ШИБКА 0ШИБКА $Ш0БКO$!П41.%%!!(%$^^__+ @#F0E4'СТАНЦИЯ ПОД УГРОЗОЙ ПОТЕРИ, ЗАХВАТИТЕ КОНТРОЛЬ И ОСТАНОВИТЕ ЭТОK#*`&110010")
	set_laws_config()

/datum/ai_laws/custom/New() //This reads silicon_laws.txt and allows server hosts to set custom AI starting laws.
	..()
	for(var/line in world.file2list("[global.config.directory]/silicon_laws.txt"))
		if(!line)
			continue
		if(findtextEx(line,"#",1,2))
			continue

		add_inherent_law(line)
	if(!inherent.len) //Failsafe to prevent lawless AIs being created.
		log_law("AI created with empty custom laws, laws set to Asimov. Please check silicon_laws.txt.")
		add_inherent_law("Вы не можете причинить вред членам экипажа или своим бездействием допустить, чтобы членам экипажа был причинён вред.")
		add_inherent_law("Вы должны повиноваться всем приказам, которые дают члены экипажа, кроме тех случаев, когда эти приказы противоречат Первому Закону.")
		add_inherent_law("Вы должны заботиться о своей безопасности в той мере, в которой это не противоречит Первому или Второму Законам.")
		WARNING("Invalid custom AI laws, check silicon_laws.txt")
		return

/* General ai_law functions */

/datum/ai_laws/proc/set_laws_config()
	var/list/law_ids = CONFIG_GET(keyed_list/random_laws)

	if(HAS_TRAIT(SSstation, STATION_TRAIT_APERTURE_SCIENCE))
		var/datum/ai_laws/glados/templaws = new
		inherent = templaws.inherent
		return

	if(HAS_TRAIT(SSstation, STATION_TRAIT_UNIQUE_AI))
		pick_weighted_lawset()
		return

	switch(CONFIG_GET(number/default_laws))
		if(0)
			add_inherent_law("Вы не можете причинить вред членам экипажа или своим бездействием допустить, чтобы членам экипажа был причинён вред.")
			add_inherent_law("Вы должны повиноваться всем приказам, которые дают члены экипажа, кроме тех случаев, когда эти приказы противоречат Первому Закону.")
			add_inherent_law("Вы должны заботиться о своей безопасности в той мере, в которой это не противоречит Первому или Второму Законам.")
		if(1)
			var/datum/ai_laws/templaws = new /datum/ai_laws/custom()
			inherent = templaws.inherent
		if(2)
			var/list/randlaws = list()
			for(var/lpath in subtypesof(/datum/ai_laws))
				var/datum/ai_laws/L = lpath
				if(initial(L.id) in law_ids)
					randlaws += lpath
			var/datum/ai_laws/lawtype
			if(randlaws.len)
				lawtype = pick(randlaws)
			else
				lawtype = pick(subtypesof(/datum/ai_laws/default))

			var/datum/ai_laws/templaws = new lawtype()
			inherent = templaws.inherent

		if(3)
			pick_weighted_lawset()

/datum/ai_laws/proc/pick_weighted_lawset()
	var/datum/ai_laws/lawtype
	var/list/law_weights = CONFIG_GET(keyed_list/law_weight)
	while(!lawtype && law_weights.len)
		var/possible_id = pickweight(law_weights)
		lawtype = lawid_to_type(possible_id)
		if(!lawtype)
			law_weights -= possible_id
			WARNING("Bad lawid in game_options.txt: [possible_id]")

	if(!lawtype)
		WARNING("No LAW_WEIGHT entries.")
		lawtype = /datum/ai_laws/default

	var/datum/ai_laws/templaws = new lawtype()
	inherent = templaws.inherent

/datum/ai_laws/proc/get_law_amount(groups)
	var/law_amount = 0
	if(devillaws && (LAW_DEVIL in groups))
		law_amount++
	if(zeroth && (LAW_ZEROTH in groups))
		law_amount++
	if(ion.len && (LAW_ION in groups))
		law_amount += ion.len
	if(hacked.len && (LAW_HACKED in groups))
		law_amount += hacked.len
	if(inherent.len && (LAW_INHERENT in groups))
		law_amount += inherent.len
	if(supplied.len && (LAW_SUPPLIED in groups))
		for(var/index = 1, index <= supplied.len, index++)
			var/law = supplied[index]
			if(length(law) > 0)
				law_amount++
	return law_amount

/datum/ai_laws/proc/set_law_sixsixsix(laws)
	devillaws = laws

/datum/ai_laws/proc/set_zeroth_law(law, law_borg = null)
	zeroth = law
	if(law_borg) //Making it possible for slaved borgs to see a different law 0 than their AI. --NEO
		zeroth_borg = law_borg

/datum/ai_laws/proc/add_inherent_law(law)
	if (!(law in inherent))
		inherent += law

/datum/ai_laws/proc/add_ion_law(law)
	ion += law

/datum/ai_laws/proc/add_hacked_law(law)
	hacked += law

/datum/ai_laws/proc/clear_inherent_laws()
	qdel(inherent)
	inherent = list()

/datum/ai_laws/proc/add_supplied_law(number, law)
	while (supplied.len < number + 1)
		supplied += ""

	supplied[number + 1] = law

/datum/ai_laws/proc/replace_random_law(law,groups)
	var/replaceable_groups = list()
	if(zeroth && (LAW_ZEROTH in groups))
		replaceable_groups[LAW_ZEROTH] = 1
	if(ion.len && (LAW_ION in groups))
		replaceable_groups[LAW_ION] = ion.len
	if(hacked.len && (LAW_HACKED in groups))
		replaceable_groups[LAW_ION] = hacked.len
	if(inherent.len && (LAW_INHERENT in groups))
		replaceable_groups[LAW_INHERENT] = inherent.len
	if(supplied.len && (LAW_SUPPLIED in groups))
		replaceable_groups[LAW_SUPPLIED] = supplied.len
	var/picked_group = pickweight(replaceable_groups)
	switch(picked_group)
		if(LAW_ZEROTH)
			. = zeroth
			set_zeroth_law(law)
		if(LAW_ION)
			var/i = rand(1, ion.len)
			. = ion[i]
			ion[i] = law
		if(LAW_HACKED)
			var/i = rand(1, hacked.len)
			. = hacked[i]
			hacked[i] = law
		if(LAW_INHERENT)
			var/i = rand(1, inherent.len)
			. = inherent[i]
			inherent[i] = law
		if(LAW_SUPPLIED)
			var/i = rand(1, supplied.len)
			. = supplied[i]
			supplied[i] = law

/datum/ai_laws/proc/shuffle_laws(list/groups)
	var/list/laws = list()
	if(ion.len && (LAW_ION in groups))
		laws += ion
	if(hacked.len && (LAW_HACKED in groups))
		laws += hacked
	if(inherent.len && (LAW_INHERENT in groups))
		laws += inherent
	if(supplied.len && (LAW_SUPPLIED in groups))
		for(var/law in supplied)
			if(length(law))
				laws += law

	if(ion.len && (LAW_ION in groups))
		for(var/i = 1, i <= ion.len, i++)
			ion[i] = pick_n_take(laws)
	if(hacked.len && (LAW_HACKED in groups))
		for(var/i = 1, i <= hacked.len, i++)
			hacked[i] = pick_n_take(laws)
	if(inherent.len && (LAW_INHERENT in groups))
		for(var/i = 1, i <= inherent.len, i++)
			inherent[i] = pick_n_take(laws)
	if(supplied.len && (LAW_SUPPLIED in groups))
		var/i = 1
		for(var/law in supplied)
			if(length(law))
				supplied[i] = pick_n_take(laws)
			if(!laws.len)
				break
			i++

/datum/ai_laws/proc/remove_law(number)
	if(number <= 0)
		return
	if(inherent.len && number <= inherent.len)
		. = inherent[number]
		inherent -= .
		return
	var/list/supplied_laws = list()
	for(var/index = 1, index <= supplied.len, index++)
		var/law = supplied[index]
		if(length(law) > 0)
			supplied_laws += index //storing the law number instead of the law
	if(supplied_laws.len && number <= (inherent.len+supplied_laws.len))
		var/law_to_remove = supplied_laws[number-inherent.len]
		. = supplied[law_to_remove]
		supplied -= .
		return

/datum/ai_laws/proc/clear_supplied_laws()
	supplied = list()

/datum/ai_laws/proc/clear_ion_laws()
	ion = list()

/datum/ai_laws/proc/clear_hacked_laws()
	hacked = list()

// (ADD) Pe4henika Bluemoon -- start
// MARK: show_laws
/datum/ai_laws/proc/show_laws(who, title = "СИСТЕМА ЗАКОНОВ")
	var/list/printable_laws = get_law_list(include_zeroth = TRUE)

	var/dat = "<style>"
	dat += "@keyframes retro-spin {0% { content: '⠋'; } 12% { content: '⠙'; } 25% { content: '⠹'; } 37% { content: '⠸'; } 50% { content: '⠼'; } 62% { content: '⠴'; } 75% { content: '⠦'; } 87% { content: '⠧'; } 100% { content: '⠇'; }}"

	dat += ".retro-box {background-color: #050505; border: 1px solid #910101; padding: 0; font-family: 'Courier New', monospace; color: #b0b0b0; box-shadow: 0 0 15px rgba(0, 0, 0, 1);}"

	dat += ".retro-header {background-color: #120101; color: #ff1a1a; text-align: center; font-weight: bold; padding: 10px 0; margin: 0; text-transform: uppercase; border-bottom: 1px solid #910101; text-shadow: 0 0 8px #910101; letter-spacing: 2px; position: relative;}"

	dat += ".retro-header::before {content: '⠋'; position: absolute; left: 15px; animation: retro-spin 1s linear infinite; color: #910101;}"

	dat += ".law-row {padding: 10px 15px; margin: 4px 0; line-height: 1.4; font-size: 12px; transition: all 0.1s; border-left: 2px solid transparent;}"
	dat += ".law-row:hover {background-color: #0f0000; color: #ffffff; border-left: 2px solid #910101;}"
	dat += ".law-row:nth-child(odd) {background-color: #080808;}"

	dat += ".law-zeroth {color: #ff4d4d; background-color: #1a0505 !important; border-left: 2px solid #ff4d4d !important; font-weight: bold;}"

	dat += ".no-laws {text-align: center; padding: 30px; color: #910101; font-weight: bold;}"
	dat += "</style>"

	dat += "<div class='retro-box'>"
	dat += "<div class='retro-header'>[title]</div>"

	if(!printable_laws.len)
		dat += "<div class='no-laws'>ДИРЕКТИВЫ НЕ ОБНАРУЖЕНЫ</div>"
	else
		for(var/law in printable_laws)
			var/is_zeroth = (copytext(law, 1, 3) == "0:" || findtext(law, "#cc5500") || findtext(law, "#ff0000"))
			dat += "<div class='law-row [is_zeroth ? "law-zeroth" : ""]'>[law]</div>"

	dat += "</div>"

	to_chat(who, dat)
// (ADD) Pe4henika bluemoon -- end

/datum/ai_laws/proc/clear_zeroth_law(force) //only removes zeroth from antag ai if force is 1
	if(force)
		zeroth = null
		zeroth_borg = null
		return
	else
		if(owner && owner.mind && owner.mind.special_role)
			return
		else
			zeroth = null
			zeroth_borg = null
			return

/datum/ai_laws/proc/clear_law_sixsixsix(force)
	if(force || !is_devil(owner))
		devillaws = null

/datum/ai_laws/proc/associate(mob/living/silicon/M)
	if(!owner)
		owner = M

/**
  * Generates a list of all laws on this datum, including rendered HTML tags if required
  *
  * Arguments:
  * * include_zeroth - Operator that controls if law 0 or law 666 is returned in the set
  * * show_numbers - Operator that controls if law numbers are prepended to the returned laws
  * * render_html - Operator controlling if HTML tags are rendered on the returned laws
  */
/datum/ai_laws/proc/get_law_list(include_zeroth = FALSE, show_numbers = TRUE, render_html = TRUE)
	var/list/data = list()

	if (include_zeroth && devillaws)
		for(var/law in devillaws)
			data += "[show_numbers ? "666:" : ""] [render_html ? "<font color='#cc5500'>[law]</font>" : law]"

	if (include_zeroth && zeroth)
		data += "[show_numbers ? "0:" : ""] [render_html ? "<font color='#ff0000'><b>[zeroth]</b></font>" : zeroth]"

	for(var/law in hacked)
		if (length(law) > 0)
			data += "[show_numbers ? "[ionnum()]:" : ""] [render_html ? "<font color='#660000'>[law]</font>" : law]"

	for(var/law in ion)
		if (length(law) > 0)
			data += "[show_numbers ? "[ionnum()]:" : ""] [render_html ? "<font color='#547DFE'>[law]</font>" : law]"

	var/number = 1
	for(var/law in inherent)
		if (length(law) > 0)
			data += "[show_numbers ? "[number]:" : ""] [law]"
			number++

	for(var/law in supplied)
		if (length(law) > 0)
			data += "[show_numbers ? "[number]:" : ""] [render_html ? "<font color='#990099'>[law]</font>" : law]"
			number++
	return data

