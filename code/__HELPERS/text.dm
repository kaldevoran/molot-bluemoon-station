/*
 * Holds procs designed to help with filtering text
 * Contains groups:
 *			SQL sanitization/formating
 *			Text sanitization
 *			Text searches
 *			Text modification
 *			Misc
 */


/*
 * SQL sanitization
 */

/proc/format_table_name(table as text)
	return CONFIG_GET(string/feedback_database) + "." + CONFIG_GET(string/feedback_tableprefix) + table

/proc/format_table_name_whitelist(table as text)
	return CONFIG_GET(string/feedback_database_whitelist) + "." + CONFIG_GET(string/feedback_tableprefix) + table

/*
 * Text sanitization
 */

//Simply removes < and > and limits the length of the message
/proc/strip_html_simple(t,limit=MAX_MESSAGE_LEN)
	var/list/strip_chars = list("<",">")
	t = copytext(t,1,limit)
	for(var/char in strip_chars)
		var/index = findtext(t, char)
		while(index)
			t = copytext(t, 1, index) + copytext(t, index+1)
			index = findtext(t, char)
	return t

/// Удаляет HTML-теги целиком, оставляя только текст между ними.
/proc/strip_html_tags(t, trim_tab = FALSE, limit = 0)
	if(!t)
		return ""
	if(limit > 0)
		t = copytext(t, 1, limit)

	// Удаляем теги вида <...>
	t = replacetext(t, regex("<\[^>\]*>", "g"), "")

	if(trim_tab)
		t = sanitize_simple(t, list("\n"=" ", "\t"=" "))

	return t

//Removes a few problematic characters
/proc/sanitize_simple(t,list/repl_chars = list("\n"="#","\t"="#"))
	for(var/char in repl_chars)
		var/index = findtext(t, char)
		while(index)
			t = copytext(t, 1, index) + repl_chars[char] + copytext(t, index + length(char))
			index = findtext(t, char, index + length(char))
	return t

/proc/sanitize_name(t,list/repl_chars = null)
	if(t == "space" || t == "floor" || t == "wall" || t == "r-wall" || t == "monkey" || t == "unknown" || t == "inactive ai")	//prevents these common metagamey names
		alert("Invalid name.")
		return ""
	return sanitize(t)

/proc/sanitize_filename(t)
	return sanitize_simple(t, list("\n"="", "\t"="", "/"="", "\\"="", "?"="", "%"="", "*"="", ":"="", "|"="", "\""="", "<"="", ">"=""))

//Runs byond's sanitization proc along-side sanitize_simple
/proc/sanitize(t,list/repl_chars = null)
	return html_encode(sanitize_simple(t,repl_chars))

//Runs sanitize and strip_html_simple
//I believe strip_html_simple() is required to run first to prevent '<' from displaying as '&lt;' after sanitize() calls byond's html_encode()
/proc/strip_html(t,limit=MAX_MESSAGE_LEN)
	return copytext((sanitize(strip_html_simple(t))),1,limit)

//Runs byond's sanitization proc along-side strip_html_simple
//I believe strip_html_simple() is required to run first to prevent '<' from displaying as '&lt;' that html_encode() would cause
/proc/adminscrub(t,limit=MAX_MESSAGE_LEN)
	return copytext((html_encode(strip_html_simple(t))),1,limit)


//Returns null if there is any bad text in the string
/proc/reject_bad_text(text, max_length = 512, ascii_only = TRUE)
	var/char_count = 0
	var/non_whitespace = FALSE
	var/lenbytes = length(text)
	var/char = ""
	for(var/i = 1, i <= lenbytes, i += length(char))
		char = text[i]
		char_count++
		if(char_count > max_length)
			return
		switch(text2ascii(char))
			if(62,60,92,47) // <, >, \, /
				return
			if(0 to 31)
				return
			if(32)
				continue		//whitespace
			if(127 to 1024)
				if(ascii_only)
					return
			if(1026 to 1039) // Ђ, Ѓ, Є, Љ ... and more
				return
			if(1106 to INFINITY)
				if(ascii_only)
					return
			else
				non_whitespace = TRUE
	if(non_whitespace)
		return text		//only accepts the text if it has some non-spaces

/// Removes the ASCII C0 control characters and DEL (0x7F) from `text`, EXCEPT tab,
/// line feed and carriage return (0x09/0x0A/0x0D). The kept whitespace has proper
/// JSON escapes and round-trips fine; the rest of the C0 range can only be escaped
/// as \uXXXX, which the DM<->TGUI json/topic bridge does not reliably preserve, so
/// any value TGUI echoes back as a lookup key (such as a custom emote panel name)
/// is silently corrupted by them. Multi-byte Unicode (e.g. Cyrillic) is preserved.
/// Returns the cleaned text.
/proc/strip_control_chars(text)
	if(!length(text))
		return text
	var/lenbytes = length(text)
	var/char = ""
	var/list/cleaned = list()
	for(var/i = 1, i <= lenbytes, i += length(char))
		char = text[i]
		var/ascii = text2ascii(char)
		if((ascii < 32 && ascii != 9 && ascii != 10 && ascii != 13) || ascii == 127)
			continue
		cleaned += char
	return jointext(cleaned, "")

/// Rebuilds a flat associative list with control characters stripped from every
/// text key, so saved data keyed by user text cannot be made permanently
/// unmatchable/undeletable by a control character that breaks the DM<->TGUI or
/// savefile round-trip. Entries whose key cleans to empty, or collides with an
/// already-cleaned key, are dropped (first one wins). Non-text keys (e.g. typepaths)
/// pass through untouched. Returns a list.
/proc/sanitize_assoc_keys(list/input)
	if(!islist(input))
		return list()
	var/list/cleaned = list()
	for(var/key in input)
		if(!istext(key))
			cleaned[key] = input[key]
			continue
		var/clean_key = strip_control_chars(key)
		if(!length(clean_key) || (clean_key in cleaned))
			continue
		cleaned[clean_key] = input[key]
	return cleaned

/// html_encode that keeps " and ' as raw readable characters (safe in HTML
/// text contexts). Dangerous chars (<, >, &) remain encoded. Use for free-form
/// user-entered text displayed in chat, names, flavor descriptions, etc.
/proc/html_encode_readable(t)
	return readd_quotes(html_encode(t))

/// Shared finalizer for stripped_* input procs: sanitize then bound to max_length.
/// Returns null on null input so callers can distinguish Cancel from empty text.
/proc/finalize_stripped_input(name, max_length, no_trim)
	if(isnull(name))
		return null
	// Control characters survive html_encode but cannot round-trip through the
	// DM<->TGUI/json bridge or savefile keys - strip them at the source.
	name = strip_control_chars(name)
	name = html_encode_readable(name)
	//trim is "outside" because html_encode can expand single symbols into multiple symbols (such as turning < into &lt;)
	return no_trim ? copytext(name, 1, max_length) : trim(name, max_length)

// Used to get a properly sanitized input, of max_length
// no_trim is self explanatory but it prevents the input from being trimed if you intend to parse newlines or whitespace.
/proc/stripped_input(mob/user, message = "", title = "", default = "", max_length=MAX_MESSAGE_LEN, no_trim=FALSE)
	return finalize_stripped_input(input(user, message, title, default) as text|null, max_length, no_trim)

// Used to get a properly sanitized multiline input, of max_length
/proc/stripped_multiline_input(mob/user, message = "", title = "", default = "", max_length=MAX_MESSAGE_LEN, no_trim=FALSE)
	var/name = input(user, message, title, default) as message|null
	if(isnull(name)) // Return null if canceled.
		return null
	return finalize_stripped_input(name, max_length, no_trim)

/**
  * stripped_multiline_input but reflects to the user instead if it's too big and returns null.
  */
/proc/stripped_multiline_input_or_reflect(mob/user, message = "", title = "", default = "", max_length=MAX_MESSAGE_LEN, no_trim=FALSE)
	var/name = input(user, message, title, default) as message|null
	if(isnull(name)) // Return null if canceled.
		return null
	if(length(name) > max_length)
		to_chat(user, name)
		to_chat(user, "<span class='danger'>^^^----- The preceeding message has been DISCARDED for being over the maximum length of [max_length]. It has NOT been sent! -----^^^</span>")
		return null
	return finalize_stripped_input(name, max_length, no_trim)

#define NO_CHARS_DETECTED 0
#define SPACES_DETECTED 1
#define SYMBOLS_DETECTED 2
#define NUMBERS_DETECTED 3
#define LETTERS_DETECTED 4

//Filters out undesirable characters from names
/proc/reject_bad_name(t_in, allow_numbers = FALSE, max_length = MAX_NAME_LEN, ascii_only = TRUE)
	if(!t_in)
		return //Rejects the input if it is null

	var/number_of_alphanumeric = 0
	var/last_char_group = NO_CHARS_DETECTED
	var/t_out = ""
	var/t_len = length(t_in)
	var/charcount = 0
	var/char = ""


	for(var/i = 1, i <= t_len, i += length(char))
		char = t_in[i]

		switch(text2ascii(char))
			// A  .. Z
			if(65 to 90)			//Uppercase Letters
				number_of_alphanumeric++
				last_char_group = LETTERS_DETECTED

			// a  .. z
			if(97 to 122)			//Lowercase Letters
				if(last_char_group == NO_CHARS_DETECTED || last_char_group == SPACES_DETECTED || last_char_group == SYMBOLS_DETECTED) //start of a word
					char = uppertext(char)
				number_of_alphanumeric++
				last_char_group = LETTERS_DETECTED

			// А  .. Я
			if(1040 to 1071)            //Uppercase Letters
				number_of_alphanumeric++
				last_char_group = LETTERS_DETECTED

			// а  .. я
			if(1072 to 1103)            //Lowercase Letters
				if(last_char_group == NO_CHARS_DETECTED || last_char_group == SPACES_DETECTED || last_char_group == SYMBOLS_DETECTED) //start of a word
					char = uppertext(char)
				number_of_alphanumeric++
				last_char_group = LETTERS_DETECTED

			// 0  .. 9
			if(48 to 57)			//Numbers
				if(last_char_group == NO_CHARS_DETECTED || !allow_numbers) //suppress at start of string
					continue
				number_of_alphanumeric++
				last_char_group = NUMBERS_DETECTED

			// '  -  .
			if(39,45,46)			//Common name punctuation
				if(last_char_group == NO_CHARS_DETECTED)
					continue
				last_char_group = SYMBOLS_DETECTED

			// ~   |   @  :  #  $  %  &  *  +
			if(126,124,64,58,35,36,37,38,42,43)			//Other symbols that we'll allow (mainly for AI)
				if(last_char_group == NO_CHARS_DETECTED || !allow_numbers) //suppress at start of string
					continue
				last_char_group = SYMBOLS_DETECTED

			//Space
			if(32)
				if(last_char_group == NO_CHARS_DETECTED || last_char_group == SPACES_DETECTED) //suppress double-spaces and spaces at start of string
					continue
				last_char_group = SPACES_DETECTED

			if(127 to INFINITY)
				if(ascii_only)
					continue
				last_char_group = SYMBOLS_DETECTED //for now, we'll treat all non-ascii characters like symbols even though most are letters

			else
				continue

		t_out += char
		charcount++
		if(charcount >= max_length)
			break

	if(number_of_alphanumeric < 2)
		return		//protects against tiny names like "A" and also names like "' ' ' ' ' ' ' '"

	if(last_char_group == SPACES_DETECTED)
		t_out = copytext_char(t_out, 1, -1) //removes the last character (in this case a space)

	for(var/bad_name in list("space","floor","wall","r-wall","monkey","unknown","inactive ai"))	//prevents these common metagamey names
		if(cmptext(t_out,bad_name))
			return	//(not case sensitive)

	return t_out

#undef NO_CHARS_DETECTED
#undef SPACES_DETECTED
#undef NUMBERS_DETECTED
#undef LETTERS_DETECTED

//html_encode helper proc that returns the smallest non null of two numbers
//or 0 if they're both null (needed because of findtext returning 0 when a value is not present)
/proc/non_zero_min(a, b)
	if(!a)
		return b
	if(!b)
		return a
	return (a < b ? a : b)

//Checks if any of a given list of needles is in the haystack
/proc/text_in_list(haystack, list/needle_list, start=1, end=0)
	for(var/needle in needle_list)
		if(findtext(haystack, needle, start, end))
			return TRUE
	return FALSE

//Like above, but case sensitive
/proc/text_in_list_case(haystack, list/needle_list, start=1, end=0)
	for(var/needle in needle_list)
		if(findtextEx(haystack, needle, start, end))
			return TRUE
	return FALSE

//Adds 'char' ahead of 'text' until there are 'count' characters total
/proc/add_leading(text, count, char = " ")
	text = "[text]"
	var/charcount = count - length_char(text)
	var/list/chars_to_add[max(charcount + 1, 0)]
	return jointext(chars_to_add, char) + text

//Adds 'char' behind 'text' until there are 'count' characters total
/proc/add_trailing(text, count, char = " ")
	text = "[text]"
	var/charcount = count - length_char(text)
	var/list/chars_to_add[max(charcount + 1, 0)]
	return text + jointext(chars_to_add, char)

//Returns a string with reserved characters and spaces before the first letter removed
/proc/trim_left(text)
	for (var/i = 1 to length(text))
		if (text2ascii(text, i) > 32)
			return copytext(text, i)
	return ""

//Returns a string with reserved characters and spaces after the last letter removed
/proc/trim_right(text)
	for (var/i = length(text), i > 0, i--)
		if (text2ascii(text, i) > 32)
			return copytext(text, 1, i + 1)
	return ""

//Returns a string with reserved characters and spaces after the first and last letters removed
//Like trim(), but very slightly faster. worth it for niche usecases
/proc/trim_reduced(text)
	var/starting_coord = 1
	var/text_len = length(text)
	for (var/i in 1 to text_len)
		if (text2ascii(text, i) > 32)
			starting_coord = i
			break

	for (var/i = text_len, i >= starting_coord, i--)
		if (text2ascii(text, i) > 32)
			return copytext(text, starting_coord, i + 1)

	if(starting_coord > 1)
		return copytext(text, starting_coord)
	return ""

//Returns a string with reserved characters and spaces before the first word and after the last word removed.
/proc/trim(text, max_length)
	if(max_length)
		text = copytext_char(text, 1, max_length)
	return trim_reduced(text)

//Returns a string with the first element of the string capitalized.
/proc/capitalize(t as text)
	. = t
	if(t)
		. = t[1]
		return uppertext(.) + copytext(t, 1 + length(.))

/proc/stringmerge(text,compare,replace = "*")
//This proc fills in all spaces with the "replace" var (* by default) with whatever
//is in the other string at the same spot (assuming it is not a replace char).
//This is used for fingerprints
	var/newtext = text
	var/text_it = 1 //iterators
	var/comp_it = 1
	var/newtext_it = 1
	var/text_length = length(text)
	var/comp_length = length(compare)
	while(comp_it <= comp_length && text_it <= text_length)
		var/a = text[text_it]
		var/b = compare[comp_it]
//if it isn't both the same letter, or if they are both the replacement character
//(no way to know what it was supposed to be)
		if(a != b)
			if(a == replace) //if A is the replacement char
				newtext = copytext(newtext, 1, newtext_it) + b + copytext(newtext, newtext_it + length(newtext[newtext_it]))
			else if(b == replace) //if B is the replacement char
				newtext = copytext(newtext, 1, newtext_it) + a + copytext(newtext, newtext_it + length(newtext[newtext_it]))
			else //The lists disagree, Uh-oh!
				return FALSE
		text_it += length(a)
		comp_it += length(b)
		newtext_it += length(newtext[newtext_it])
	return newtext

/proc/stringpercent(text,character = "*")
//This proc returns the number of chars of the string that is the character
//This is used for detective work to determine fingerprint completion.
	if(!text || !character)
		return FALSE
	var/count = 0
	var/lentext = length(text)
	var/a = ""
	for(var/i = 1, i <= lentext, i += length(a))
		a = text[i]
		if(a == character)
			count++
	return count

/proc/reverse_text(text = "")
	var/new_text = ""
	var/lentext = length(text)
	var/letter = ""
	for(var/i = 1, i <= lentext, i += length(letter))
		letter = text[i]
		new_text = letter + new_text
	return new_text

GLOBAL_LIST_INIT(zero_character_only, list("0"))
GLOBAL_LIST_INIT(hex_characters, list("0","1","2","3","4","5","6","7","8","9","a","b","c","d","e","f"))
GLOBAL_LIST_INIT(alphabet, list("a","b","c","d","e","f","g","h","i","j","k","l","m","n","o","p","q","r","s","t","u","v","w","x","y","z"))
GLOBAL_LIST_INIT(binary, list("0","1"))
/proc/random_string(length, list/characters)
	. = ""
	for(var/i=1, i<=length, i++)
		. += pick(characters)

/proc/repeat_string(times, string="")
	. = ""
	for(var/i=1, i<=times, i++)
		. += string

/proc/random_short_color()
	return random_string(3, GLOB.hex_characters)

/proc/random_color()
	return random_string(6, GLOB.hex_characters)

//merges non-null characters (3rd argument) from "from" into "into". Returns result
//e.g. into = "Hello World"
//     from = "Seeya______"
//     returns"Seeya World"
//The returned text is always the same length as into
//This was coded to handle DNA gene-splicing.
/proc/merge_text(into, from, null_char="_")
	. = ""
	if(!istext(into))
		into = ""
	if(!istext(from))
		from = ""
	var/null_ascii = istext(null_char) ? text2ascii(null_char, 1) : null_char
	var/copying_into = FALSE
	var/char = ""
	var/start = 1
	var/end_from = length(from)
	var/end_into = length(into)
	var/into_it = 1
	var/from_it = 1
	while(from_it <= end_from && into_it <= end_into)
		char = from[from_it]
		if(text2ascii(char) == null_ascii)
			if(!copying_into)
				. += copytext(from, start, from_it)
				start = into_it
				copying_into = TRUE
		else
			if(copying_into)
				. += copytext(into, start, into_it)
				start = from_it
				copying_into = FALSE
		into_it += length(into[into_it])
		from_it += length(char)

	if(copying_into)
		. += copytext(into, start)
	else
		. += copytext(from, start, from_it)
		if(into_it <= end_into)
			. += copytext(into, into_it)

//finds the first occurrence of one of the characters from needles argument inside haystack
//it may appear this can be optimised, but it really can't. findtext() is so much faster than anything you can do in byondcode.
//stupid byond :(
/proc/findchar(haystack, needles, start=1, end=0)
	var/char = ""
	var/len = length(needles)
	for(var/i = 1, i <= len, i += length(char))
		char = needles[i]
		. = findtextEx(haystack, char, start, end)
		if(.)
			return
	return FALSE

/proc/parsemarkdown_basic_step1(t, limited=FALSE)
	if(length(t) <= 0)
		return

	// This parses markdown with no custom rules

	// Escape backslashed

	t = replacetext(t, "$", "$-")
	t = replacetext(t, "\\\\", "$1")
	t = replacetext(t, "\\**", "$2")
	t = replacetext(t, "\\*", "$3")
	t = replacetext(t, "\\__", "$4")
	t = replacetext(t, "\\_", "$5")
	t = replacetext(t, "\\^", "$6")
	t = replacetext(t, "\\((", "$7")
	t = replacetext(t, "\\))", "$8")
	t = replacetext(t, "\\|", "$9")
	t = replacetext(t, "\\%", "$0")

	// Escape  single characters that will be used

	t = replacetext(t, "!", "$a")

	// Parse hr and small

	if(!limited)
		t = replacetext(t, "((", "<font size=\"1\">")
		t = replacetext(t, "))", "</font>")
		t = replacetext(t, regex("(-){3,}", "gm"), "<hr>")
		t = replacetext(t, regex("^\\((-){3,}\\)$", "gm"), "$1")

		// Parse lists

		var/list/tlist = splittext(t, "\n")
		var/tlistlen = tlist.len
		var/listlevel = -1
		var/singlespace = -1 // if 0, double spaces are used before asterisks, if 1, single are
		for(var/i = 1, i <= tlistlen, i++)
			var/line = tlist[i]
			var/count_asterisk = length(replacetext(line, regex("\[^\\*\]+", "g"), ""))
			if(count_asterisk % 2 == 1 && findtext(line, regex("^\\s*\\*", "g"))) // there is an extra asterisk in the beggining

				var/count_w = length(replacetext(line, regex("^( *)\\*.*$", "g"), "$1")) // whitespace before asterisk
				line = replacetext(line, regex("^ *(\\*.*)$", "g"), "$1")

				if(singlespace == -1 && count_w == 2)
					if(listlevel == 0)
						singlespace = 0
					else
						singlespace = 1

				if(singlespace == 0)
					count_w = count_w % 2 ? round(count_w / 2 + 0.25) : count_w / 2

				line = replacetext(line, regex("\\*", ""), "<li>")
				while(listlevel < count_w)
					line = "<ul>" + line
					listlevel++
				while(listlevel > count_w)
					line = "</ul>" + line
					listlevel--

			else while(listlevel >= 0)
				line = "</ul>" + line
				listlevel--

			tlist[i] = line
		// end for

		t = jointext(tlist, "\n")

		while(listlevel >= 0)
			t += "</ul>"
			listlevel--

	else
		t = replacetext(t, "((", "")
		t = replacetext(t, "))", "")

	// Parse headers

	t = replacetext(t, regex("^#(?!#) ?(.+)$", "gm"), "<h2>$1</h2>")
	t = replacetext(t, regex("^##(?!#) ?(.+)$", "gm"), "<h3>$1</h3>")
	t = replacetext(t, regex("^###(?!#) ?(.+)$", "gm"), "<h4>$1</h4>")
	t = replacetext(t, regex("^#### ?(.+)$", "gm"), "<h5>$1</h5>")

	// Parse most rules

	t = replacetext(t, regex("\\*(\[^\\*\]*)\\*", "g"), "<i>$1</i>")
	t = replacetext(t, regex("_(\[^_\]*)_", "g"), "<i>$1</i>")
	t = replacetext(t, "<i></i>", "!")
	t = replacetext(t, "</i><i>", "!")
	t = replacetext(t, regex("\\!(\[^\\!\]+)\\!", "g"), "<b>$1</b>")
	t = replacetext(t, regex("\\^(\[^\\^\]+)\\^", "g"), "<font size=\"4\">$1</font>")
	t = replacetext(t, regex("\\|(\[^\\|\]+)\\|", "g"), "<center>$1</center>")
	t = replacetext(t, "!", "</i><i>")

	return t

/proc/parsemarkdown_basic_step2(t)
	if(length(t) <= 0)
		return

	// Restore the single characters used

	t = replacetext(t, "$a", "!")

	// Redo the escaping

	t = replacetext(t, "$1", "\\")
	t = replacetext(t, "$2", "**")
	t = replacetext(t, "$3", "*")
	t = replacetext(t, "$4", "__")
	t = replacetext(t, "$5", "_")
	t = replacetext(t, "$6", "^")
	t = replacetext(t, "$7", "((")
	t = replacetext(t, "$8", "))")
	t = replacetext(t, "$9", "|")
	t = replacetext(t, "$0", "%")
	t = replacetext(t, "$-", "$")

	return t

/proc/parsemarkdown_basic(t, limited=FALSE)
	t = parsemarkdown_basic_step1(t, limited)
	t = parsemarkdown_basic_step2(t)
	return t

/proc/parsemarkdown(t, mob/user=null, limited=FALSE)
	if(length(t) <= 0)
		return

	// Premanage whitespace

	t = replacetext(t, regex("\[^\\S\\r\\n \]", "g"), "  ")

	t = parsemarkdown_basic_step1(t)

	t = replacetext(t, regex("%s(?:ign)?(?=\\s|$)", "igm"), user ? "<font face=\"[SIGNFONT]\"><i>[user.real_name]</i></font>" : "<span class=\"paper_field\"></span>")
	t = replacetext(t, regex("%f(?:ield)?(?=\\s|$)", "igm"), "<span class=\"paper_field\"></span>")

	t = parsemarkdown_basic_step2(t)

	// Manage whitespace

	t = replacetext(t, regex("(?:\\r\\n?|\\n)", "g"), "<br>")

	t = replacetext(t, "  ", "&nbsp;&nbsp;")

	// Done

	return t

/proc/text2charlist(text)
	var/char = ""
	var/lentext = length(text)
	. = list()
	for(var/i = 1, i <= lentext, i += length(char))
		char = text[i]
		. += char

/proc/rot13(text = "")
	var/lentext = length(text)
	var/char = ""
	var/ascii = 0
	. = ""
	for(var/i = 1, i <= lentext, i += length(char))
		char = text[i]
		ascii = text2ascii(char)
		switch(ascii)
			if(65 to 77, 97 to 109) //A to M, a to m
				ascii += 13
			if(78 to 90, 110 to 122) //N to Z, n to z
				ascii -= 13
		. += ascii2text(ascii)

//Takes a list of values, sanitizes it down for readability and character count,
//then exports it as a json file at data/npc_saves/[filename].json.
//As far as SS13 is concerned this is write only data. You can't change something
//in the json file and have it be reflected in the in game item/mob it came from.
//(That's what things like savefiles are for) Note that this list is not shuffled.
/proc/twitterize(list/proposed, filename, cullshort = 1, storemax = 1000)
	if(!islist(proposed) || !filename || !CONFIG_GET(flag/log_twitter))
		return

	//Regular expressions are, as usual, absolute magic
	//Any characters outside of 32 (space) to 126 (~) because treating things you don't understand as "magic" is really stupid
	var/regex/all_invalid_symbols = new(@"[^ -~]{1}")

	var/list/accepted = list()
	for(var/string in proposed)
		if(findtext(string,GLOB.is_website) || findtext(string,GLOB.is_email) || findtext(string,all_invalid_symbols) || !findtext(string,GLOB.is_alphanumeric))
			continue
		var/buffer = ""
		var/early_culling = TRUE
		var/lentext = length(string)
		var/let = ""

		for(var/pos = 1, pos <= lentext, pos += length(let))
			let = string[pos]
			if(!findtext(let, GLOB.is_alphanumeric))
				continue
			early_culling = FALSE
			buffer = copytext(string, pos)
			break
		if(early_culling) //Never found any letters! Bail!
			continue

		var/punctbuffer = ""
		var/cutoff = 0
		lentext = length_char(buffer)
		for(var/pos = 1, pos <= lentext, pos++)
			let = copytext_char(buffer, -pos, -pos + 1)
			if(!findtext(let, GLOB.is_punctuation)) //This won't handle things like Nyaaaa!~ but that's fine
				break
			punctbuffer += let
			cutoff += length(let)
		if(punctbuffer) //We clip down excessive punctuation to get the letter count lower and reduce repeats. It's not perfect but it helps.
			var/exclaim = FALSE
			var/question = FALSE
			var/periods = 0
			lentext = length(punctbuffer)
			for(var/pos = 1, pos <= lentext, pos += length(let))
				let = punctbuffer[pos]
				if(!exclaim && findtext(let, "!"))
					exclaim = TRUE
					if(question)
						break
				if(!question && findtext(let, "?"))
					question = TRUE
					if(exclaim)
						break
				if(!exclaim && !question && findtext(let, ".")) //? and ! take priority over periods
					periods += 1
			if(exclaim)
				if(question)
					punctbuffer = "?!"
				else
					punctbuffer = "!"
			else if(question)
				punctbuffer = "?"
			else if(periods > 1)
				punctbuffer = "..."
			else
				punctbuffer = "" //Grammer nazis be damned
			buffer = copytext(buffer, 1, -cutoff) + punctbuffer
		lentext = length_char(buffer)
		if(!buffer || lentext > 280 || lentext <= cullshort || (buffer in accepted))
			continue

		accepted += buffer

	var/log = file("data/npc_saves/[filename].json") //If this line ever shows up as changed in a PR be very careful you aren't being memed on
	var/list/oldjson = list()
	var/list/oldentries = list()
	if(fexists(log))
		oldjson = json_decode(file2text(log))
		oldentries = oldjson["data"]
	if(length(oldentries))
		for(var/string in accepted)
			for(var/old in oldentries)
				if(string == old)
					oldentries.Remove(old) //Line's position in line is "refreshed" until it falls off the in game radar
					break

	var/list/finalized = list()
	finalized = accepted.Copy() + oldentries.Copy() //we keep old and unreferenced phrases near the bottom for culling
	listclearnulls(finalized)
	if(length(finalized) > storemax)
		finalized.Cut(storemax + 1)
	fdel(log)

	var/list/tosend = list()
	tosend["data"] = finalized
	WRITE_FILE(log, json_encode(tosend))

//Used for applying byonds text macros to strings that are loaded at runtime
/proc/apply_text_macros(string)
	var/next_backslash = findtext(string, "\\")
	if(!next_backslash)
		return string

	var/leng = length(string)

	var/next_space = findtext(string, " ", next_backslash + length(string[next_backslash]))
	if(!next_space)
		next_space = leng - next_backslash

	if(!next_space)	//trailing bs
		return string

	var/base = next_backslash == 1 ? "" : copytext(string, 1, next_backslash)
	var/macro = lowertext(copytext(string, next_backslash + length(string[next_space]), next_space))
	var/rest = next_backslash > leng ? "" : copytext(string, next_space + length(string[next_space]))

	//See https://secure.byond.com/docs/ref/info.html#/DM/text/macros
	switch(macro)
		//prefixes/agnostic
		if("the")
			rest = text("\the []", rest)
		if("a")
			rest = text("\a []", rest)
		if("an")
			rest = text("\an []", rest)
		if("proper")
			rest = text("\proper []", rest)
		if("improper")
			rest = text("\improper []", rest)
		if("roman")
			rest = text("\roman []", rest)
		//postfixes
		if("th")
			base = text("[]\th", rest)
		if("s")
			base = text("[]\s", rest)
		if("he")
			base = text("[]\he", rest)
		if("she")
			base = text("[]\she", rest)
		if("his")
			base = text("[]\his", rest)
		if("himself")
			base = text("[]\himself", rest)
		if("herself")
			base = text("[]\herself", rest)
		if("hers")
			base = text("[]\hers", rest)

	. = base
	if(rest)
		. += .(rest)

//Replacement for the \th macro when you want the whole word output as text (first instead of 1st)
/proc/thtotext(number)
	if(!isnum(number))
		return
	switch(number)
		if(1)
			return "first"
		if(2)
			return "second"
		if(3)
			return "third"
		if(4)
			return "fourth"
		if(5)
			return "fifth"
		if(6)
			return "sixth"
		if(7)
			return "seventh"
		if(8)
			return "eighth"
		if(9)
			return "ninth"
		if(10)
			return "tenth"
		if(11)
			return "eleventh"
		if(12)
			return "twelfth"
		else
			return "[number]\th"


/proc/random_capital_letter()
	return uppertext(pick(GLOB.alphabet))

/proc/unintelligize(message)
	var/regex/word_boundaries = regex(@"\b[\S]+\b", "g")
	var/prefix = message[1]
	if(prefix == ";")
		message = copytext(message, 1 + length(prefix))
	else if(prefix in list(":", "#"))
		prefix += message[1 + length(prefix)]
		message = copytext(message, length(prefix))
	else
		prefix = ""

	var/list/rearranged = list()
	while(word_boundaries.Find(message))
		var/cword = word_boundaries.match
		if(length(cword))
			rearranged += cword
	shuffle_inplace(rearranged)
	return "[prefix][jointext(rearranged, " ")]"


#define is_alpha(X) ((text2ascii(X) <= 122) && (text2ascii(X) >= 97))
#define is_digit(X) ((length(X) == 1) && (length(text2num(X)) == 1))

/// Slightly expensive proc to scramble a message using equal probabilities of character replacement from a list. DOES NOT SUPPORT HTML!
/proc/scramble_message_replace_chars(original, replaceprob = 25, list/replacementchars = list("$", "@", "!", "#", "%", "^", "&", "*"), replace_letters_only = FALSE, replace_whitespace = FALSE)
	var/list/out = list()
	var/static/list/whitespace = list(" ", "\n", "\t")
	var/char = ""
	for(var/i = 1, i <= length(original), i += length(char))
		char = original[i]
		if(!replace_whitespace && (char in whitespace))
			out += char
			continue
		if(replace_letters_only && (!ISINRANGE(char, 65, 90) && !ISINRANGE(char, 97, 122)))
			out += char
			continue
		out += prob(replaceprob)? pick(replacementchars) : char
	return out.Join("")

/proc/readable_corrupted_text(text)
	var/list/corruption_options = list("..", "£%", "~~\"", "!!", "*", "^", "$!", "-", "}", "?")
	var/corrupted_text = ""
	for(var/letter_index = 1; letter_index <= length_char(text); letter_index++)	// Have every letter have a chance of creating corruption on either side
		var/letter = copytext_char(text, letter_index, letter_index + 1)	// Small chance of letters being removed in place of corruption - still overall readable
		if(prob(15))
			corrupted_text += pick(corruption_options)
		if(prob(95))
			corrupted_text += letter
		else
			corrupted_text += pick(corruption_options)
	if(prob(15))
		corrupted_text += pick(corruption_options)
	return corrupted_text

/proc/format_text(text)
	if(!text)
		return ""
	return replacetext(replacetext(text,"\proper ",""),"\improper ","")

/// Removes all non-alphanumerics from the text, keep in mind this can lead to id conflicts
/proc/sanitize_css_class_name(name)
	var/static/regex/regex = new(@"[^a-zA-Z0-9]","g")
	return replacetext(name, regex, "")

/proc/parse_zone(zone)
	var/static/list/zone_names = list(
		BODY_ZONE_PRECISE_R_HAND = "right hand",
		BODY_ZONE_PRECISE_L_HAND = "left hand",
		BODY_ZONE_L_ARM = "left arm",
		BODY_ZONE_R_ARM = "right arm",
		BODY_ZONE_L_LEG = "left leg",
		BODY_ZONE_R_LEG = "right leg",
		BODY_ZONE_PRECISE_L_FOOT = "left foot",
		BODY_ZONE_PRECISE_R_FOOT = "right foot",
	)
	return zone_names[zone] || zone

/proc/ru_parse_zone(zone)	// Именительный
	var/static/list/zone_names = list(
		BODY_ZONE_PRECISE_R_HAND = "правая кисть",
		BODY_ZONE_PRECISE_L_HAND = "левая кисть",
		BODY_ZONE_L_ARM = "левая рука",
		BODY_ZONE_R_ARM = "правая рука",
		BODY_ZONE_L_LEG = "левая нога",
		BODY_ZONE_R_LEG = "правая нога",
		BODY_ZONE_PRECISE_L_FOOT = "левая ступня",
		BODY_ZONE_PRECISE_R_FOOT = "правая ступня",
		"chest" = "грудь",
		"mouth" = "рот",
		"groin" = "пах",
		"head" = "голова",
		"eyes" = "глаза",
	)
	return zone_names[zone] || zone

/proc/ru_kogo_zone(zone)	// Винительный
	var/static/list/zone_names = list(
		BODY_ZONE_PRECISE_R_HAND = "правую кисть",
		BODY_ZONE_PRECISE_L_HAND = "левую кисть",
		BODY_ZONE_L_ARM = "левую руку",
		BODY_ZONE_R_ARM = "правую руку",
		BODY_ZONE_L_LEG = "левую ногу",
		BODY_ZONE_R_LEG = "правую ногу",
		BODY_ZONE_PRECISE_L_FOOT = "левую ступню",
		BODY_ZONE_PRECISE_R_FOOT = "правую ступню",
		"chest" = "грудь",
		"mouth" = "рот",
		"groin" = "пах",
		"head" = "голову",
	)
	return zone_names[zone] || zone

/proc/ru_gde_zone(zone)	// Дательный // Я поменял значения как у ru_parse_zone(), чтобы можно было использовать в коде.
	var/static/list/zone_names = list(
		BODY_ZONE_PRECISE_R_HAND = "правой кисти",
		BODY_ZONE_PRECISE_L_HAND = "левой кисти",
		BODY_ZONE_L_ARM = "левой руке",
		BODY_ZONE_R_ARM = "правой руке",
		BODY_ZONE_L_LEG = "левой ноге",
		BODY_ZONE_R_LEG = "правой ноге",
		BODY_ZONE_PRECISE_L_FOOT = "левой ступне",
		BODY_ZONE_PRECISE_R_FOOT = "правой ступне",
		"chest" = "груди",
		"mouth" = "ротовой полости",
		"groin" = "паховой области",
		"head" = "голове",
	)
	return zone_names[zone] || zone

/proc/ru_otkuda_zone(zone)	// Родительный
	var/static/list/zone_names = list(
		"правая кисть" = "правой кисти",
		"левая кисть" = "левой кисти",
		"левая рука" = "левой руки",
		"правая рука" = "правой руки",
		"левая нога" = "левой ноги",
		"правая нога" = "правой ноги",
		"левая ступня" = "левой ступни",
		"правая ступня" = "правой ступни",
		"грудь" = "груди",
		"рот" = "ротовой полости",
		"пах" = "паховой области",
		"голова" = "головы",
	)
	return zone_names[zone] || zone

/proc/ru_chem_zone(zone)	// Творительный
	var/static/list/zone_names = list(
		"правая кисть" = "правой кистью",
		"левая кисть" = "левой кистью",
		"левая рука" = "левой рукой",
		"правая рука" = "правой рукой",
		"левая нога" = "левой ногой",
		"правая нога" = "правой ногой",
		"левая ступня" = "левой ступней",
		"правая ступня" = "правой ступней",
		"грудь" = "грудью",
		"рот" = "ртом",
		"пах" = "пахом",
		"голова" = "головой",
	)
	return zone_names[zone] || zone

/proc/ru_exam_parse_zone(zone)
	var/static/list/zone_names = list(
		"chest" = "грудь",
		"mouth" = "рот",
		"groin" = "пах",
		"head" = "голова",
	)
	return zone_names[zone] || zone

/proc/ru_intent(intent)
	var/static/list/intent_names = list(
		INTENT_HELP = "помогать",
		INTENT_GRAB = "хватать",
		INTENT_DISARM = "толкать",
		INTENT_HARM = "вредить",
	)
	return intent_names[intent] || intent

/proc/uplink_to_ru_conversion(uplink)
	var/static/list/uplink_names = list(
		"PDA" = "ПДА",
		"Radio" = "Наушник",
		"Pen" = "Ручка",
		"Implant" = "Имплант",
	)
	return uplink_names[uplink] || uplink

/proc/backpack_to_ru_conversion(backpack)
	var/static/list/backpack_names = list(
		"Grey Backpack" = "Серый рюкзак",
		"Grey Satchel" = "Серая сумка",
		"Grey Duffel Bag" = "Серый вещмешок",
		"Leather Satchel" = "Кожаная сумка",
		"Department Backpack" = "Рюкзак отдела",
		"Department Satchel" = "Сумка отдела",
		"Department Duffel Bag" = "Вещмешок отдела",
	)
	return backpack_names[backpack] || backpack

///Returns a string based on the weight class define used as argument
/proc/weight_class_to_text(w_class)
	switch(w_class)
		if(WEIGHT_CLASS_TINY)
			. = "крохотного"
		if(WEIGHT_CLASS_SMALL)
			. = "маленького"
		if(WEIGHT_CLASS_NORMAL)
			. = "нормального"
		if(WEIGHT_CLASS_BULKY)
			. = "большого"
		if(WEIGHT_CLASS_HUGE)
			. = "огромного"
		if(WEIGHT_CLASS_GIGANTIC)
			. = "гигантского"
		else
			. = ""

/proc/ru_comms(freq)
	var/static/list/comms_names = list(
		"Common" = "Основной",
		"Security" = "Безопасность",
		"Engineering" = "Инженерия",
		"Command" = "Командование",
		"Science" = "Научный",
		"Medical" = "Медбей",
		"Supply" = "Снабжение",
		"Service" = "Обслуживание",
		"Exploration" = "Рейнджеры",
		"AI Private" = "Приватный ИИ",
		"Syndicate" = "Синдикат",
		"CentCom" = "ЦентКом",
		"Red Team" = "Красные",
		"Blue Team" = "Синие",
		"Tarkov" = "Тарков",
	)
	return comms_names[freq] || freq

/proc/r_json_decode(text) //now I'm stupid
	for(var/s in GLOB.rus_unicode_conversion_hex)
		text = replacetext(text, "\\u[GLOB.rus_unicode_conversion_hex[s]]", s)
	return json_decode(text)

//Adds 'u' number of zeros ahead of the text 't'
/proc/add_zero(t, u)
	while(length(t) < u)
		t = "0[t]"
	return t

/proc/rainbow_span(text)
	var/static/list/rainbow_colors = list("#FF0000", "#FF7F00", "#FFFF00", "#00FF00", "#0000FF", "#4B0082", "#9400D3")
	var/result = ""
	var/color_index = 1
	for(var/i = 1, i <= length_char(text), i++)
		var/char = copytext_char(text, i, i + 1)
		result += "<font color='[rainbow_colors[color_index]]'>[char]</font>"
		color_index = (color_index % length(rainbow_colors)) + 1
	return result

/proc/pink_shimmer_span(text)
	var/static/list/pink_shades = list("#FF69B4", "#FF85C0", "#FFB6C1", "#FFC0CB", "#FFA0C0", "#FF8DA1", "#FF69B4")
	var/result = ""
	var/color_index = 1
	for(var/i = 1, i <= length_char(text), i++)
		var/char = copytext_char(text, i, i + 1)
		result += "<font color='[pink_shades[color_index]]'>[char]</font>"
		color_index = (color_index % length(pink_shades)) + 1
	return result
